chapter Successors
Define the successor pseudo-function which will form the basis of our inductive definition of the natural numbers.

term successor (n)
  name (a Successor)
  format (n^+)
  (= _ ∪ n singleton n)

theorem Element of Successor
premise ∈ n successor m
prove ∈ n ∪ m singleton m
  via = successor m ∪ m singleton m
prove ∨ ∈ n m ∈ n singleton m
assume ∈ n singleton m {
  prove = n m
}
prove ∨ ∈ n m = n m
qed

theorem A Set Is an Element of Its Successor
prove ∈ n singleton n
prove ∈ n ∪ n singleton n
prove ∈ n successor n
  via = successor n ∪ n singleton n
qed

theorem An Element of a Set Is an Element of Its Successor
premise ∈ n m
prove ∈ n ∪ m singleton m
prove ∈ n successor m
  via = successor m ∪ m singleton m
qed

theorem A Set Is a Subset of Its Successor
take x {
  assume ∈ x n {
    prove ∈ x successor n
  }
}
prove ⊆ n successor n
qed

theorem Successors Are Unique
premise = successor n successor m
prove ∈ n successor m
  via ∈ n successor n
prove ∨ ∈ n m = n m
prove ∈ m successor n
  via ∈ m successor m
prove ∨ ∈ m n = m n
assume = m n {
  prove = n m
}
prove ∨ ∈ m n = n m
assume ¬ = n m {
  prove ∈ n m
  prove ∈ m n
  prove ∧ ∈ n m ∈ m n
  prove ¬ ∧ ∈ n m ∈ m n
  prove ⊥
}
prove = n m
qed
