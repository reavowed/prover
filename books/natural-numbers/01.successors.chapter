chapter Successors
Define the successor pseudo-function which will form the basis of our inductive definition of the natural numbers.

term successor (a) format (successor(a)) (= _ ∪ a singleton a)

theorem Element of Successor
premise ∈ a successor b
prove = successor b ∪ b singleton b
prove ∈ a ∪ b singleton b
prove ∨ ∈ a b ∈ a singleton b
assume ∈ a singleton b {
  prove = a b
}
prove ∨ ∈ a b = a b
qed

theorem A Set Is an Element of Its Successor
prove = successor a ∪ a singleton a
prove ∈ a singleton a
prove ∈ a ∪ a singleton a
prove ∈ a successor a
qed

theorem An Element of a Set Is an Element of Its Successor
premise ∈ a b
prove = successor b ∪ b singleton b
prove ∈ a ∪ b singleton b
prove ∈ a successor b
qed

theorem A Set Is a Subset of Its Successor
assume ∈ x a {
  prove ∈ x successor a
}
prove ⊆ a successor a
qed

theorem Successors Are Non-Empty
prove ∈ a successor a
prove ¬ = successor a ∅
qed

theorem Successors Are Unique
premise = successor a successor b
prove ∈ a successor a
prove ∈ a successor b
prove ∨ ∈ a b = a b
prove ∈ b successor b
prove ∈ b successor a
prove ∨ ∈ b a = b a
assume = b a {
  prove = a b
}
prove ∨ ∈ b a = a b
assume ¬ = a b {
  prove ∈ a b
  prove ∈ b a
  prove ∧ ∈ a b ∈ b a
  prove ¬ ∧ ∈ a b ∈ b a
}
prove = a b
qed
