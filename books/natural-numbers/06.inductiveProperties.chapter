chapter Inductive Properties
Some basic inductive properties of ℕ.

theorem A Natural Number Is Either 0 or a Successor
premise ∈ n ℕ
prove = 0 0
prove ∨ = 0 0 ∃ m ∧ ∈ m ℕ = successor m 0
assume ∧ ∈ a ℕ ∨ = a 0 ∃ m ∧ ∈ m ℕ = successor m a {
  prove = successor a successor a
  prove ∧ ∈ a ℕ = successor a successor a
  prove ∃ m ∧ ∈ m ℕ = successor m successor a
  prove ∨ = successor a 0 ∃ m ∧ ∈ m ℕ = successor m successor a
}
prove ∨ = n 0 ∃ m ∧ ∈ m ℕ = successor m n
qed

theorem Every Element of a Natural Number Is a Natural Number
premise ∈ n ℕ
premise ∈ m n
prove ∀ x ¬ ∈ x ∅
prove = 0 ∅
prove ∀ x ¬ ∈ x 0
prove ∀ x → ∈ x 0 ∈ x ℕ
assume ∧ ∈ a ℕ ∀ x → ∈ x a ∈ x ℕ {
  assume ∈ x a {
    prove ∈ x ℕ
  }
  assume = x a {
    prove ∈ x ℕ
  }
  assume ∈ x successor a {
    prove ∨ ∈ x a = x a
    prove ∈ x ℕ
  }
  prove ∀ x → ∈ x successor a ∈ x ℕ
}
prove ∀ x → ∈ x n ∈ x ℕ
prove ∈ m ℕ
qed

theorem A Natural Number Is a Subset of the Natural Numbers
premise ∈ n ℕ
assume ∈ m n {
  prove ∈ m ℕ
}
prove ⊆ n ℕ
qed

theorem A Natural Number Either Is 0 or Contains 0
premise ∈ n ℕ
prove = 0 0
prove ∨ = 0 0 ∈ 0 0
assume ∧ ∈ m ℕ ∨ = m 0 ∈ 0 m {
  assume = m 0 {
    prove ∈ m successor m
    prove ∈ 0 successor m
  }
  assume ∈ 0 m {
    prove ∈ 0 successor m
  }
  prove ∈ 0 successor m
  prove ∨ = successor m 0 ∈ 0 successor m
}
prove ∨ = n 0 ∈ 0 n
qed

theorem A Natural Number Either Contains Or Is The Successor of an Element
premise ∈ n ℕ
premise ∈ m n
prove ∀ x ¬ ∈ x ∅
prove = 0 ∅
prove ∀ x ¬ ∈ x 0
prove ∀ x → ∈ x 0 ∨ ∈ successor x 0 = successor x 0
assume ∧ ∈ a ℕ ∀ x → ∈ x a ∨ ∈ successor x a = successor x a {
  assume ∈ x successor a {
    prove ∨ ∈ x a = x a
    assume ∈ x a {
      prove ∨ ∈ successor x a = successor x a
      assume ∈ successor x a {
        prove ∈ successor x successor a
      }
      assume = successor x a {
        prove ∈ a successor a
        prove ∈ successor x successor a
      }
      prove ∈ successor x successor a
    }
    assume = x a {
      prove = successor x successor x
      prove = successor x successor a
    }
    prove ∨ ∈ successor x successor a = successor x successor a
  }
  prove ∀ x → ∈ x successor a ∨ ∈ successor x successor a = successor x successor a
}
prove ∀ x → ∈ x n ∨ ∈ successor x n = successor x n
prove ∨ ∈ successor m n = successor m n
qed

theorem Natural Number Trichotomy
premise ∈ n ℕ
premise ∈ m ℕ
prove ∨ = n 0 ∈ 0 n
prove ∨ ∈ n 0 ∨ = n 0 ∈ 0 n
assume ∧ ∈ a ℕ ∨ ∈ n a ∨ = n a ∈ a n {
  assume ∈ n a {
    prove ∈ n successor a
    prove ∨ ∈ n successor a ∨ = n successor a ∈ successor a n
  }
  assume = n a {
    prove ∈ n successor n
    prove ∈ n successor a
    prove ∨ ∈ n successor a ∨ = n successor a ∈ successor a n
  }
  assume ∈ a n {
    prove ∨ ∈ successor a n = successor a n
    prove ∨ = successor a n ∈ successor a n
    assume = successor a n {
      prove = n successor a
    }
    prove ∨ = n successor a ∈ successor a n
    prove ∨ ∈ n successor a ∨ = n successor a ∈ successor a n
  }
  assume ∨ = n a ∈ a n {
    prove ∨ ∈ n successor a ∨ = n successor a ∈ successor a n
  }
  prove ∨ ∈ n successor a ∨ = n successor a ∈ successor a n
}
prove ∨ ∈ n m ∨ = n m ∈ m n
qed

theorem Natural Number Transitivity
premise ∈ n ℕ
assume ∧ ∈ m' 0 ∈ x' m' {
  prove ∀ x ¬ ∈ x ∅
  prove ¬ ∈ m' ∅
  prove = 0 ∅
  prove ¬ ∈ m' 0
  prove ∈ x' 0
}
prove ∀ m → ∧ ∈ m 0 ∈ x' m ∈ x' 0
prove ∀ x ∀ m → ∧ ∈ m 0 ∈ x m ∈ x 0
assume ∧ ∈ n' ℕ ∀ x ∀ m → ∧ ∈ m n' ∈ x m ∈ x n' {
  assume ∧ ∈ m' successor n' ∈ x' m' {
    prove ∨ ∈ m' n' = m' n'
    assume ∈ m' n' {
      prove ∧ ∈ m' n' ∈ x' m'
      prove ∀ m → ∧ ∈ m n' ∈ x' m ∈ x' n'
      prove ∈ x' n'
    }
    assume = m' n' {
      prove ∈ x' n'
    }
    prove ∈ x' n'
    prove ∈ x' successor n'
  }
  prove ∀ m → ∧ ∈ m successor n' ∈ x' m ∈ x' successor n'
  prove ∀ x ∀ m → ∧ ∈ m successor n' ∈ x m ∈ x successor n'
}
prove ∀ x ∀ m → ∧ ∈ m n ∈ x m ∈ x n
qed

theorem Natural Number Transitivity
premise ∈ n ℕ
premise ∈ m n
premise ∈ x m
prove ∀ x' ∀ m' → ∧ ∈ m' n ∈ x' m' ∈ x' n
prove ∀ m' → ∧ ∈ m' n ∈ x m' ∈ x n
prove ∧ ∈ m n ∈ x m
prove ∈ x n
qed

theorem A Natural Number Contains the Predecessor of an Element
premise ∈ n ℕ
premise ∈ successor m n
prove ∈ m successor m
prove ∈ m n
qed

theorem Natural Number Contains Element If Successor Contains Successor
premise ∈ n ℕ
premise ∈ successor m successor n
prove ∈ successor n ℕ
prove ∈ m successor n
prove ∨ ∈ m n = m n
assume = m n {
  prove ¬ ∈ successor m successor m
  prove ¬ ∈ successor m successor n
}
prove ¬ = m n
prove ∈ m n
qed
