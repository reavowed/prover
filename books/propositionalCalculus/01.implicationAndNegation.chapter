chapter Implication and Negation
We start by introducing our two fundamental connectives, → and ¬, and proving some basic facts about them.

axiom Repetition
premise φ
conclusion φ

statement → (φ ψ)

axiom Deduction
premise proves φ ψ
conclusion → φ ψ

axiom Modus Ponens
premise → φ ψ
premise φ
conclusion ψ

theorem Implication Is Reflexive
assume φ {
  prove φ
}
prove → φ φ
qed

theorem Implication Is Transitive
premise → φ ψ
premise → ψ χ
assume φ {
  prove ψ
  prove χ
}
prove → φ χ
qed

statement ¬ (φ)

axiom Proof by Contradiction
premise proves φ ψ
premise proves φ ¬ ψ
conclusion ¬ φ

axiom Proof by Contradiction (Inverted)
premise proves ¬ φ ψ
premise proves ¬ φ ¬ ψ
conclusion φ

theorem Proof by Self-Contradiction
premise proves φ ¬ φ
assume φ {
  prove φ
}
prove ¬ φ
qed

theorem Proof by Self-Contradiction (Inverted)
premise proves ¬ φ φ
assume ¬ φ {
  prove ¬ φ
}
prove φ
qed

theorem Add Double Negative
premise φ
assume ¬ φ {
  prove φ
  prove ¬ φ
}
prove ¬ ¬ φ
qed

theorem Remove Double Negative
premise ¬ ¬ φ
assume ¬ φ {
  prove ¬ φ
  prove ¬ ¬ φ
}
prove φ
qed

theorem Principle of Explosion
premise φ
premise ¬ φ
assume ¬ ψ {
  prove φ
  prove ¬ φ
}
prove ψ
qed

theorem Anything Implies A True Statement
premise φ
assume ψ {
  prove φ
}
prove → ψ φ
qed

theorem A False Statement Implies Anything
premise ¬ φ
assume φ {
  prove ψ
}
prove → φ ψ
qed

theorem A False Statement Implies Anything
premise φ
assume ¬ φ {
  prove ψ
}
prove → ¬ φ ψ
qed

theorem Modus Tollens
premise → φ ψ
premise ¬ ψ
assume φ {
  prove ψ
  prove ¬ ψ
}
prove ¬ φ
qed

theorem Contraposition
premise → φ ψ
assume ¬ ψ {
  prove ¬ φ
}
prove → ¬ ψ ¬ φ
qed

theorem Proof by Cases
premise → φ ψ
premise → ¬ φ ψ
assume ¬ ψ {
  prove ¬ φ
  prove ¬ ¬ φ
}
prove ψ
qed
