Define an relation between two sets.

type relation R (A)
  format (on A)
  definition (⊆ R × A A)

property reflexive on relation ~ A
  definition (∀ a → ∈ $0 A ∈ orderedPair $0 $0 ~)

property symmetric on relation ~ A
  definition (∀ a → ∈ $0 A ∀ b → ∈ $0 A → ∈ orderedPair $$0 $0 ~ ∈ orderedPair $0 $$0 ~)

property transitive on relation ~ A
  definition (∀ a → ∈ $0 A ∀ b → ∈ $0 A ∀ c → ∈ $0 A → ∧ ∈ orderedPair $$$0 $$0 ~ ∈ orderedPair $$0 $0 ~ ∈ orderedPair $$$0 $0 ~)

type equivalenceRelation ≈ (A)
  format (on A)
  name (equivalence relation)
  definition (∧ ∧ ∧ relation ≈ A reflexiveRelation ≈ A symmetricRelation ≈ A transitiveRelation ≈ A)

term equivalenceClass (a ≈)
  name (Equivalence Class)
  format ([a])
  premises (equivalenceRelation ≈ A, ∈ a A)
  (= $0 comprehension x A ∈ orderedPair a $0 ≈)

term equivalenceQuotient (A ≈)
  name (Quotient)
  format (A/≈) requires-brackets
  premises (equivalenceRelation ≈ A)
  (= $0 replacementSet a A equivalenceClass $0 ≈)
