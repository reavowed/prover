chapter Existential Quantifier
Introduce the existential quantifier, ∃.

statement ∃ (x φ) format ((∃x)φ) definition (¬ ∀ x ¬ φ)

theorem Existence
premise sub y x φ
assume ∀ x ¬ φ {
  prove ¬ sub y x φ
}
prove ¬ ∀ x ¬ φ
prove ∃ x φ
qed

theorem Proof by Existence
premise ∃ x φ
premise proves sub y x φ ψ
assume ¬ ψ {
  prove ¬ sub y x φ
  prove ∀ x ¬ φ
  prove ¬ ∀ x ¬ φ
}
prove ψ
qed

theorem Proof by Existence
premise ∃ x φ
premise proves φ ψ
assume ¬ ψ {
  prove ¬ φ
  prove ∀ x ¬ φ non-distinct (x φ)
  prove ¬ ∀ x ¬ φ
}
prove ψ
qed

theorem Not Exists Not Is For All
premise ¬ ∃ x ¬ φ
assume ¬ φ {
  prove ∃ x ¬ φ
}
prove φ
prove ∀ x φ
qed

theorem Not Exists Is For All Not
premise ¬ ∃ x φ
assume ¬ ∀ x ¬ φ {
  prove ∃ x φ
}
prove ∀ x ¬ φ
qed

theorem For All Not Is Not Exists
premise ∀ x ¬ φ
assume ∃ x φ {
  prove ¬ ∀ x ¬ φ
}
prove ¬ ∃ x φ
qed

theorem Existence Equivalence
premise ∃ x φ
premise ∀ x ↔ φ ψ
let y sub y x φ {
  prove sub y x ψ
  prove ∃ x ψ
}
qed
