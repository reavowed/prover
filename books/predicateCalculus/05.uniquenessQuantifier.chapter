chapter Uniqueness Quantifier
Introduce the uniqueness quantifier, ∃!.

statement ∃! (x φ)
  format ((∃!x)φ)
  definition (∧ ∃ x φ ∀ y ∀ z → ∧ sub y x φ sub z x φ = y z)
  distinct-variables (y φ, z φ)

theorem Prove Uniqueness
premise ∃ x φ
premise proves ∧ sub y x φ sub z x φ = y z
prove → ∧ sub y x φ sub z x φ = y z
prove ∀ z → ∧ sub y x φ sub z x φ = y z
prove ∀ y ∀ z → ∧ sub y x φ sub z x φ = y z
prove ∧ ∃ x φ ∀ y ∀ z → ∧ sub y x φ sub z x φ = y z
prove ∃! x φ
qed

theorem Every Set Is Unique
prove ∃ y = y x
assume ∧ = a x = b x {
  prove = a x
  prove = b x
  prove = a b
}
prove ∃! y = y x
qed

theorem Alternate Definition of Uniqueness
premise ∀ x ↔ φ = x y
prove = y y
prove sub y x φ
prove ∃ x φ
assume ∧ sub a x φ sub b x φ {
  prove sub a x φ
  prove sub b x φ
  prove ↔ sub a x φ = a y
  prove ↔ sub b x φ = b y
  prove = a y
  prove = b y
  prove = a b
}
prove ∃! x φ
qed

theorem Uniqueness by Two Cases
premise ⊕ φ ψ
assume φ {
  prove ¬ ψ
  prove ↔ ∨ ∧ φ = z x ∧ ψ = z y = z x
  prove ∀ z ↔ ∨ ∧ φ = z x ∧ ψ = z y = z x
  prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
}
assume ψ {
  prove ¬ φ
  prove ↔ ∨ ∧ φ = z x ∧ ψ = z y = z y
  prove ∀ z ↔ ∨ ∧ φ = z x ∧ ψ = z y = z y
  prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
}
prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
qed

theorem Uniqueness by Two Cases
prove ⊕ φ ¬ φ
prove ∃! z ∨ ∧ φ = z x ∧ ¬ φ = z y
qed
