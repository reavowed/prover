chapter Equality
Introduce equality and its associated rules of inference.

predicate = 2
axiom equalityIsReflexive "Equality is Reflexive" = 1 1
rule eliminateEquals = 1 2 & sub 1 3 1 ⇒ sub 2 3 1

theorem reverseEquality Reverse Equality
premise = 1 2
equalityIsReflexive 1
eliminateEquals p1 1 = 3 1 3
qed

theorem equalityIsSymmetric Equality Is Symmetric
assume = 1 2
  reverseEquality f.a
introduceImplication f.1
assume = 2 1
  reverseEquality f.a
introduceImplication f.1
introduceEquivalence 1 2
qed

theorem equalityIsTransitive Equality Is Transitive
premise = 1 2
premise = 2 3
eliminateEquals p2 p1 = 1 4 4
qed

theorem equalityIsTransitiveLeft Equality Is Transitive (Left)
premise = 1 2
premise = 1 3
reverseEquality p1
equalityIsTransitive 1 p2
qed

theorem equalityIsTransitiveRight Equality Is Transitive (Right)
premise = 1 2
premise = 3 2
reverseEquality p2
equalityIsTransitive p1 1
qed

theorem eliminateEqualsReverse Eliminate Equals (Reverse)
premise = 2 1
premise sub 1 3 1
reverseEquality p1
eliminateEquals 1 p2
qed

theorem equalityEquivalence Equality Equivalence
premise = 1 2
assume sub 1 3 1
  eliminateEquals p1 f.a
introduceImplication f.1
assume sub 2 3 1
  eliminateEqualsReverse p1 f.a
introduceImplication f.1
introduceEquivalence 1 2
qed

theorem quantifiedEqualityReplacement Quantified Equality Replacement
premise sub 1 2 1
assume = 2' 1
  eliminateEqualsReverse f.a p1
introduceImplication f.1
introduceForall 1 → = 2 1 1 2' 2
qed

theorem eliminateQuantifiedEquality Eliminate Quantified Equality
premise ∃ 1 ∧ = 2 1 sub 1 3 1
assume ∧ = 2 1' sub 1' 3 1
  eliminateAndLeft f.a
  eliminateAndRight f.a
  eliminateEqualsReverse f.1 f.2
introduceImplication f.3
eliminateExists p1 1 1'
qed

theorem reverseEqualityInAntecedent Reverse Equality in Antecedent
premise → = 1 2 1
assume = 2 1
  reverseEquality f.a
  eliminateImplication p1 f.1
introduceImplication f.2
qed

theorem thereExistsASetEqualToAnySet There Exists a Set Equal to Any Set
equalityIsReflexive 1
introduceExists 1 = 2 1 1 2
qed

theorem eliminateRename Eliminate Rename
premise → = 1 2 1
introduceExistsInAntecedent p1 = 3 2 1 3
thereExistsASetEqualToAnySet 3 2
eliminateImplication 1 2
qed
