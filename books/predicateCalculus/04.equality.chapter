chapter Equality
Introduce equality and its associated rules of inference.

statement = (x y)

axiom Equality Is Reflexive
conclusion = x x

axiom Substitution of Equals
premise = x y
premise sub x z φ
conclusion sub y z φ

theorem Reverse Equality
premise = x y
prove = x x
prove = y x
qed

theorem Substitution of Equals
premise = x y
premise sub y z φ
prove = y x
prove sub x z φ
qed

theorem Equivalence of Substituted Equals
premise = x y
assume sub x z φ {
  prove sub y z φ
}
assume sub y z φ {
  prove sub x z φ
}
prove ↔ sub x z φ sub y z φ
qed

theorem Equality Is Symmetric
assume = x y {
  prove = y x
}
assume = y x {
  prove = x y
}
prove ↔ = x y = y x
qed

theorem Reverse Equality Inside Negation
premise ¬ = x y
assume = y x {
  prove = x y
}
prove ¬ = y x
qed

theorem Equality Is Transitive
premise = x y
premise = y z
prove = x z
qed

theorem Sets Are Equal If Always Equal to the Same Thing
premise ∀ z ↔ = z x = z y
prove = x x
prove = x y
qed

theorem Every Set Exists
prove = x x
prove ∃ y = y x
qed

theorem Exclusive Or for Equality
premise ∨ = x y = x z
premise ¬ = y z
assume ∧ = x y = x z {
  prove = x y
  prove = x z
  prove = y z
}
prove ¬ ∧ = x y = x z
prove ⊕ = x y = x z
qed

theorem Proof by Renamed Set
premise proves = a x φ
prove ∃ y = y x
prove φ
qed
