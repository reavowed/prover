chapter Comprehension
Introduce set comprehension, {x ∈ X | φ}.

theorem Comprehensions Exist for Empty Sets
premise ¬ ∃ y ∧ ∈ y X sub y x φ
prove ∀ y ¬ ∧ ∈ y X sub y x φ
prove ∀ y ↔ ∈ y ∅ ∧ ∈ y X sub y x φ
prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist for Non-Empty Sets Lemma
premise ∧ ∈ a X sub a x φ
prove ∈ a X
prove sub a x φ
assume ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a {
  prove ∈ x X
  prove ∨ ∧ φ = y x ∧ ¬ φ = y a
  assume φ {
    prove ↔ ∨ ∧ φ = y x ∧ ¬ φ = y a = y x
    prove = y x
    prove ∈ y X
    prove sub y x φ
    prove ∧ ∈ y X sub y x φ
  }
  assume ¬ φ {
    prove ↔ ∨ ∧ φ = y x ∧ ¬ φ = y a = y a
    prove = y a
    prove ∈ y X
    prove sub y x φ
    prove ∧ ∈ y X sub y x φ
  }
  prove ∧ ∈ y X sub y x φ
}
assume ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a {
  prove ∧ ∈ y X sub y x φ
}
assume ∧ ∈ y X sub y x φ {
  prove ∈ y X
  prove sub y x φ
  prove = y y
  prove ∧ sub y x φ = y y
  prove ∨ ∧ sub y x φ = y y ∧ ¬ sub y x φ = y a
  prove ∧ ∈ y X ∨ ∧ sub y x φ = y y ∧ ¬ sub y x φ = y a
  prove ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a
}
prove ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
prove ∀ y ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist for Non-Empty Sets
premise ∃ y ∧ ∈ y X sub y x φ
assume ∧ ∈ a X sub a x φ {
  prove ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove → ∈ x X ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∀ x → ∈ x X ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∃ Y ∀ y ↔ ∈ y Y ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∀ y ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
  prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist
assume ¬ ∃ y ∧ ∈ y X sub y x φ {
  prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
assume ∃ y ∧ ∈ y X sub y x φ {
  prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Are Unique
prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

term comprehension (x X φ) format ({x ∈ X | φ}) (∀ y ↔ ∈ y _ ∧ ∈ y X sub y x φ)
