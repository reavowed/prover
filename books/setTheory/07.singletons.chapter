chapter Singletons
Establish the existence of the singleton set {x}.

theorem singletonsExist Singletons Exist
thereExistsAUniqueSetEqualToAnySet 3 1
anythingImpliesTrue 1 ∈ 4' powerSet ∅
introduceForall 2 → ∈ 4 powerSet ∅ ∃! 3 = 3 1 4' 4
replacementInference 3 2
theEmptySetIsAnElementOfAnyPowerSet ∅
introduceExists 5 ∈ 4 powerSet ∅ ∅ 4
redunantAndInExists 6 = 3' 1
introduceForall 7 ↔ ∃ 4 ∧ ∈ 4 powerSet ∅ = 3 1 = 3 1 3' 3
substituteExistenceMembershipCondition 4 8
qed

theorem singletonsAreUnique Singletons are Unique
singletonsExist 2 3 1
setWithMembershipConditionIsUnique 1
qed

term singleton (x) format ({x}) (∀ y ↔ ∈ y singleton x = y x)

theorem characteriseElementsOfSingleton Characterise Elements of a Singleton
definition-singleton 2 1
eliminateForall 1 2
qed

theorem setIsElementOfSingleton A Set is an Element of Its Singleton
characteriseElementsOfSingleton 1 1
equalityIsReflexive 1
modusPonensEquivalenceRight 1 2
qed

theorem elementOfSingletonIsSet An Element of a Singleton is the Set Itself
premise ∈ 2 singleton 1
characteriseElementsOfSingleton 2 1
modusPonensEquivalenceLeft 1 p1
qed

theorem singletonIsNotEmpty A Singleton Is Not Empty
setIsElementOfSingleton 1
introduceExists 1 ∈ 2 singleton 1 1 2
setWithSomeElementIsNonEmpty 2
qed

theorem singletonIsSubsetIfSetIsElement Singleton Is a Subset If the Set Is an Element
premise ∈ 1 2
assume ∈ 3' singleton 1
  elementOfSingletonIsSet f.a
  eliminateEqualsReverse f.1 p1 ∈ 4 2 4
introduceImplication f.2
introduceForall 1 → ∈ 3 singleton 1 ∈ 3 2 3' 3
apply-⊆ 2
qed

theorem singletonInPowerSet Singleton Is in Power Set
premise ∈ 1 2
singletonIsSubsetIfSetIsElement p1
subsetIsElementOfPowerSet 1
qed

theorem setsAreEqualIfTheirSingletonsAreEqual Sets Are Equal If Their Singletons Are Equal
premise = singleton 1 singleton 2
setIsElementOfSingleton 1
eliminateEquals p1 1 ∈ 1 3 3
elementOfSingletonIsSet 2
qed

theorem subsetsOfSingleton Subsets of a Singleton
assume ∈ 2 powerSet singleton 1
  elementOfPowerSetIsSubset f.a
  unapply-⊆ f.1 3
  definition-singleton 3 1
  implicationThenEquivalenceInsideForall f.2 f.3
  assume ∃ 3 ∈ 3 2
    assume ∈ 3' 2
      modusPonensInsideForall f.4 f.f.f.a 3'
      eliminateEquals f.f.f.1 f.f.f.a ∈ 4 2 4
    introduceImplication f.f.f.2
    eliminateExists f.f.a f.f.1 3'
    singletonIsSubsetIfSetIsElement f.f.2
    subsetIsAntisymmetric f.1 f.f.3
    introduceOrRight f.f.4 = 2 ∅
  introduceImplication f.f.5
  assume ¬ ∃ 3 ∈ 3 2
    notExistsIsForallNot f.f.a
    setWithNoElementsIsTheEmptySet f.f.1
    introduceOrLeft f.f.2 = 2 singleton 1
  introduceImplication f.f.3
  proofByCases f.5 f.6
introduceImplication f.7
assume = 2 ∅
  theEmptySetIsAnElementOfAnyPowerSet singleton 1
  eliminateEqualsReverse f.a f.1 ∈ 3 powerSet singleton 1 3
introduceImplication f.2
assume = 2 singleton 1
  anySetIsAnElementOfItsPowerSet singleton 1
  eliminateEqualsReverse f.a f.1 ∈ 3 powerSet singleton 1 3
introduceImplication f.2
combineCases 2 3
introduceEquivalence 1 4
qed
