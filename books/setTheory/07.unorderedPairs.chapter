chapter Unordered Pairs
Establish the existence of the unordered pair {a, b}.

theorem Unordered Pair Existence Lemma
assume ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b {
  let x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b {
    assume ∧ = x singleton ∅ = y a {
      prove ∨ = y a = y b
    }
    assume ∧ = x ∅ = y b {
      prove ∨ = y a = y b
    }
    prove ∨ = y a = y b
  }
}
assume ∨ = y a = y b {
  assume = y a {
    prove ∈ singleton ∅ powerSet singleton ∅
    prove = singleton ∅ singleton ∅
    prove ∧ ∈ singleton ∅ powerSet singleton ∅ ∨ ∧ = singleton ∅ singleton ∅ = y a  ∧ = singleton ∅ ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
  }
  assume = y b {
    prove ∈ ∅ powerSet singleton ∅
    prove = ∅ ∅
    prove ∧ ∈ ∅ powerSet singleton ∅ ∨ ∧ = ∅ singleton ∅ = y a ∧ = ∅ ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
  }
  prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
}
prove ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
qed

theorem Unordered Pair Existence
assume ∈ x powerSet singleton ∅ {
  prove ⊕ = x singleton ∅ = x ∅
  prove ∃! y ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
}
prove ∀ x → ∈ x powerSet singleton ∅ ∃! y ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
prove ∃ z ∀ y ↔ ∈ y z ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
prove ∃ z ∀ y ↔ ∈ y z ∨ = y a = y b
prove ∃! z ∀ y ↔ ∈ y z ∨ = y a = y b
qed

term pair (a b)
  name (Unordered Pair)
  format ({a, b})
  (∀ x ↔ ∈ x _ ∨ = x a = x b)

theorem Elements of Unordered Pair
premise ∈ c pair a b
prove ∀ x ↔ ∈ x pair a b ∨ = x a = x b
prove ∨ = c a = c b
qed

theorem First Element of Unordered Pair
prove ∀ x ↔ ∈ x pair a b ∨ = x a = x b
prove = a a
prove ∨ = a a = a b
prove ∈ a pair a b
qed

theorem Second Element of Unordered Pair
prove ∀ x ↔ ∈ x pair a b ∨ = x a = x b
prove = b b
prove ∨ = b a = b b
prove ∈ b pair a b
qed

theorem An Unordered Pair Is Not the Empty Set
prove ∈ a pair a b
prove ¬ = pair a b ∅
qed

theorem Reverse Unordered Pair
prove ∀ x ↔ ∈ x pair a b ∨ = x a = x b
prove ∀ x ↔ ∈ x pair b a ∨ = x b = x a
prove ∀ x ↔ ∨ = x a = x b ∨ = x b = x a
prove = pair a b pair b a
qed

theorem Repeated Pair Is Singleton
prove ∀ x ↔ ∈ x pair a a ∨ = x a = x a
prove ∀ x ↔ ∨ = x a = x a = x a
prove ∀ x ↔ ∈ x pair a a = x a
prove ∀ x ↔ ∈ x singleton a = x a
prove = pair a a singleton a
qed

theorem Sets Are Equal If Pair Is Singleton
premise = pair a b singleton c
prove ∈ a pair a b
prove ∈ a singleton c
prove = a c
qed

theorem Sets Are Equal If Pair Is Singleton
premise = pair a b singleton c
prove ∈ b pair a b
prove ∈ b singleton c
prove = b c
qed

theorem Sets Are Equal If Pair Is Singleton
premise = pair a b singleton c
prove = a c
prove = b c
prove = a b
qed

theorem Unordered Pair Is Subset
premise ∈ a S
premise ∈ b S
prove ∀ x ↔ ∈ x pair a b ∨ = x a = x b
assume = x a {
  prove ∈ x S
}
assume = x b {
  prove ∈ x S
}
assume ∨ = x a = x b {
  prove ∈ x S
}
prove ∀ x → ∨ = x a = x b ∈ x S
prove ∀ x → ∈ x pair a b ∈ x S
prove ⊆ pair a b S
qed

theorem Unordered Pair Is Element of Power Set
premise ∈ a S
premise ∈ b S
prove ⊆ pair a b S
prove ∈ pair a b powerSet S
qed
