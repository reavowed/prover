chapter Unordered Pairs
Establish the existence of the unordered pair {x, y}.

theorem Unordered Pair Existence Lemma
assume ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b {
  prove ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
  assume ∧ = x singleton ∅ = y a {
    prove = y a
    prove ∨ = y a = y b
  }
  assume ∧ = x ∅ = y b {
    prove = y b
    prove ∨ = y a = y b
  }
  prove ∨ = y a = y b
}
assume ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b {
  prove ∨ = y a = y b
}
assume ∨ = y a = y b {
  assume = y a {
    prove = singleton ∅ singleton ∅
    prove ∧ = singleton ∅ singleton ∅ = y a
    prove ∨ ∧ = singleton ∅ singleton ∅ = y a ∧ = singleton ∅ ∅ = y b
    prove ∈ singleton ∅ powerSet singleton ∅
    prove ∧ ∈ singleton ∅ powerSet singleton ∅ ∨ ∧ = singleton ∅ singleton ∅ = y a  ∧ = singleton ∅ ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
  }
  assume = y b {
    prove = ∅ ∅
    prove ∧ = ∅ ∅ = y b
    prove ∨ ∧ = ∅ singleton ∅ = y a ∧ = ∅ ∅ = y b
    prove ∈ ∅ powerSet singleton ∅
    prove ∧ ∈ ∅ powerSet singleton ∅ ∨ ∧ = ∅ singleton ∅ = y a ∧ = ∅ ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
  }
  prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
}
prove ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
qed

theorem Unordered Pairs Exist
assume ∈ x powerSet singleton ∅ {
  prove ⊕ = x singleton ∅ = x ∅
  prove ∃! y ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
}
prove → ∈ x powerSet singleton ∅ ∃! y ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
prove ∀ x → ∈ x powerSet singleton ∅ ∃! y ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
prove ∃ X ∀ y ↔ ∈ y X ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x singleton ∅ = y a ∧ = x ∅ = y b ∨ = y a = y b
prove ∃ X ∀ y ↔ ∈ y X ∨ = y a = y b
qed

theorem Unordered Pairs are Unique
prove ∃ X ∀ y ↔ ∈ y X ∨ = y a = y b
prove ∃! X ∀ y ↔ ∈ y X ∨ = y a = y b
qed

term pair (x y)
  name (Unordered Pair)
  format ({x, y})
  (∀ z ↔ ∈ z _ ∨ = z x = z y)

theorem Elements of Unordered Pair
premise ∈ z pair x y
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove ∨ = z x = z y
qed

theorem First Element of Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove = x x
prove ∨ = x x = x y
prove ∈ x pair x y
qed

theorem Second Element of Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove = y y
prove ∨ = y x = y y
prove ∈ y pair x y
qed

theorem Reverse Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove ∀ z ↔ ∈ z pair y x ∨ = z y = z x
prove ∀ z ↔ ∨ = z x = z y ∨ = z y = z x
prove = pair x y pair y x
qed

theorem Repeated Pair Is Singleton
prove ∀ y ↔ ∈ y pair x x ∨ = y x = y x
prove ∀ y ↔ ∨ = y x = y x = y x
prove ∀ y ↔ ∈ y pair x x = y x
prove ∀ y ↔ ∈ y singleton x = y x
prove = pair x x singleton x
qed

theorem Sets Are Equal If Pair Is Singleton
premise = pair a b singleton x
prove ∈ a pair a b
prove ∈ a singleton x
prove = a x
prove ∈ b pair a b
prove ∈ b singleton x
prove = b x
prove ∧ = a x = b x
qed

theorem Unordered Pair Is Subset
premise ∈ x X
premise ∈ y X
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
assume = z x {
  prove ∈ z X
}
assume = z y {
  prove ∈ z X
}
assume ∨ = z x = z y {
  prove ∈ z X
}
prove → ∨ = z x = z y ∈ z X
prove ∀ z → ∨ = z x = z y ∈ z X
prove ∀ z → ∈ z pair x y ∈ z X
prove ⊆ pair x y X
qed

theorem Unordered Pair Is Element of Power Set
premise ∈ x X
premise ∈ y X
prove ⊆ pair x y X
prove ∈ pair x y powerSet X
qed
