chapter Unordered Pairs
Establish the existence of the unordered pair {x,y}.

theorem Unordered Pair Lemma 1
premise ⊕ φ ψ
prove ∨ φ ψ
assume φ {
  prove ¬ ψ
  prove ↔ ∨ ∧ φ = z x ∧ ψ = z y = z x
  prove ∀ z ↔ ∨ ∧ φ = z x ∧ ψ = z y = z x
  prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
}
assume ψ {
  prove ¬ φ
  prove ↔ ∨ ∧ φ = z x ∧ ψ = z y = z y
  prove ∀ z ↔ ∨ ∧ φ = z x ∧ ψ = z y = z y
  prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
}
prove ∃! z ∨ ∧ φ = z x ∧ ψ = z y
qed

theorem Unordered Pair Lemma 2
assume ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b {
  prove ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
  assume ∧ = x ∅ = y a {
    prove = y a
    prove ∨ = y a = y b
  }
  assume ∧ = x singleton ∅ = y b {
    prove = y b
    prove ∨ = y a = y b
  }
  prove ∨ = y a = y b
}
assume ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b {
  prove ∨ = y a = y b
}
assume ∨ = y a = y b {
  assume = y a {
    prove = ∅ ∅
    prove ∧ = ∅ ∅ = y a
    prove ∨ ∧ = ∅ ∅ = y a  ∧ = ∅ singleton ∅ = y b
    prove ∈ ∅ powerSet singleton ∅
    prove ∧ ∈ ∅ powerSet singleton ∅ ∨ ∧ = ∅ ∅ = y a  ∧ = ∅ singleton ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
  }
  assume = y b {
    prove = singleton ∅ singleton ∅
    prove ∧ = singleton ∅ singleton ∅ = y b
    prove ∨ ∧ = singleton ∅ ∅ = y a  ∧ = singleton ∅ singleton ∅ = y b
    prove ∈ singleton ∅ powerSet singleton ∅
    prove ∧ ∈ singleton ∅ powerSet singleton ∅ ∨ ∧ = singleton ∅ ∅ = y a  ∧ = singleton ∅ singleton ∅ = y b
    prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
  }
  prove ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
}
prove ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b ∨ = y a = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b ∨ = y a = y b
qed

theorem Unordered Pairs Exist
assume ∈ x powerSet singleton ∅ {
  prove ⊆ x singleton ∅
  prove ⊕ = x ∅ = x singleton ∅
  prove ∃! y ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
}
prove → ∈ x powerSet singleton ∅ ∃! y ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
prove ∀ x → ∈ x powerSet singleton ∅ ∃! y ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
prove ∃ X ∀ y ↔ ∈ y X ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b
prove ∀ y ↔ ∃ x ∧ ∈ x powerSet singleton ∅ ∨ ∧ = x ∅ = y a ∧ = x singleton ∅ = y b ∨ = y a = y b
prove ∃ X ∀ y ↔ ∈ y X ∨ = y a = y b
qed

theorem Unordered Pairs are Unique
prove ∃ X ∀ y ↔ ∈ y X ∨ = y a = y b
prove ∃! X ∀ y ↔ ∈ y X ∨ = y a = y b
qed

term pair (x y)
  name (Unordered Pair)
  format ({x,y})
  (∀ z ↔ ∈ z _ ∨ = z x = z y)

theorem Elements of Unordered Pair
premise ∈ z pair x y
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove ∨ = z x = z y
qed

theorem First Element of Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove = x x
prove ∨ = x x = x y
prove ∈ x pair x y
qed

theorem Second Element of Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove = y y
prove ∨ = y x = y y
prove ∈ y pair x y
qed

theorem Reverse Unordered Pair
prove ∀ z ↔ ∈ z pair x y ∨ = z x = z y
prove ∀ z ↔ ∈ z pair y x ∨ = z y = z x
prove ∀ z ↔ ∨ = z x = z y ∨ = z y = z x
prove = pair x y pair y x
qed

theorem Repated Pair Is Singleton
prove ∀ y ↔ ∈ y pair x x ∨ = y x = y x
prove ∀ y ↔ ∨ = y x = y x = y x
prove ∀ y ↔ ∈ y pair x x = y x
prove ∀ y ↔ ∈ y singleton x = y x
prove = pair x x singleton x
qed

theorem Sets Are Equal If Pair Is Singleton
premise = pair a b singleton x
prove ∈ a pair a b
prove ∈ a singleton x
prove = a x
prove ∈ b pair a b
prove ∈ b singleton x
prove = b x
prove ∧ = a x = b x
qed
