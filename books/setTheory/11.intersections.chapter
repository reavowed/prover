chapter Intersections
Arbitrary and binary intersections.

theorem Intersection over a Class
premise ∃ x φ
let a sub a x φ {
  assume ∀ x → φ ∈ y x {
    prove ∈ y a
  }
  prove ↔ ∧ ∈ y a ∀ x → φ ∈ y x ∀ x → φ ∈ y x
  prove ∀ y ↔ ∧ ∈ y a ∀ x → φ ∈ y x ∀ x → φ ∈ y x
  let S = S comprehension z a ∀ x → φ ∈ z x {
    prove ∀ y ↔ ∈ y S ∧ ∈ y a ∀ x → φ ∈ y x
    prove ∀ y ↔ ∈ y S ∀ x → φ ∈ y x
    prove ∃ Y ∀ y ↔ ∈ y Y ∀ x → φ ∈ y x
  }
}
qed

theorem Arbitrary Intersections Are Unique
premise ¬ = a ∅
prove ∃ x ∈ x a
prove ∃ Y ∀ y ↔ ∈ y Y ∀ x → ∈ x a ∈ y x
prove ∃! Y ∀ y ↔ ∈ y Y ∀ x → ∈ x a ∈ y x
qed

term intersection (a)
  name (Arbitrary Intersection)
  format (⋂a)
  premises (¬ = a ∅)
  (∀ y ↔ ∈ y _ ∀ x → ∈ x a ∈ y x)
  distinct-variables (x y, x a, y a)

term ∩ (a b)
  (= _ intersection pair a b)

theorem Membership Condition for Binary Intersection
prove ∈ a pair a b
prove ¬ = pair a b ∅
prove ∀ x ↔ ∈ x intersection pair a b ∀ y → ∈ y pair a b ∈ x y
assume ∀ y → ∈ y pair a b ∈ x y {
  prove ∈ a pair a b
  prove ∈ x a
  prove ∈ b pair a b
  prove ∈ x b
  prove ∧ ∈ x a ∈ x b
}
assume ∧ ∈ x a ∈ x b {
  prove ∈ x a
  prove ∈ x b
  assume = y a {
    prove ∈ x y
  }
  assume = y b {
    prove ∈ x y
  }
  assume ∨ = y a = y b {
    prove ∈ x y
  }
  prove ∀ y → ∨ = y a = y b ∈ x y
  prove ∀ y ↔ ∈ y pair a b ∨ = y a = y b
  prove ∀ y → ∈ y pair a b ∈ x y
}
prove ↔ ∀ y → ∈ y pair a b ∈ x y ∧ ∈ x a ∈ x b
prove ∀ x ↔ ∀ y → ∈ y pair a b ∈ x y ∧ ∈ x a ∈ x b
prove ∀ x ↔ ∈ x intersection pair a b ∧ ∈ x a ∈ x b
prove = ∩ a b intersection pair a b
prove ∀ x ↔ ∈ x ∩ a b ∧ ∈ x a ∈ x b
qed

theorem Binary Intersection Is Symmetric
prove ∀ x ↔ ∈ x ∩ a b ∧ ∈ x a ∈ x b
prove ∀ x ↔ ∈ x ∩ b a ∧ ∈ x b ∈ x a
prove ∀ x ↔ ∧ ∈ x a ∈ x b ∧ ∈ x b ∈ x a
prove = ∩ a b ∩ b a
qed

theorem An Element of Two Sets Is an Element of Their Intersection
premise ∈ c a
premise ∈ c b
prove ∀ x ↔ ∈ x ∩ a b ∧ ∈ x a ∈ x b
prove ∧ ∈ c a ∈ c b
prove ∈ c ∩ a b
qed

theorem A Set Can Only Be an Element of One of a Disjoint Pair
premise = ∩ a b ∅
premise ∈ c a
assume ∈ c b {
  prove ∈ c ∩ a b
  prove ¬ = ∩ a b ∅
}
prove ¬ ∈ c b
qed

theorem A Set Can Only Be an Element of One of a Disjoint Pair
premise = ∩ a b ∅
premise ∈ c b
prove = ∩ a b ∩ b a
prove = ∩ b a ∅
prove ¬ ∈ c a
qed
