chapter Set Membership and Extensionality
Introduce the set membership predicate ∈ and the Axiom of Extensionality, and explore their consequences for equality.

statement ∈ (term term) () (1 2) ()

theorem equalSetsHaveSameMembers Equal Sets Have Same Members
premise = 1 2
equalityEquivalence p1 ∈ 3' 1 1
introduceForall 1 ↔ ∈ 3 1 ∈ 3 2 3' 3
qed

axiom extensionality Extensionality ∀ 1 ∀ 2 → ∀ 3 ↔ ∈ 3 1 ∈ 3 2 = 1 2

theorem setsWithSameMembersEqual Sets Are Equal If They Have The Same Members
premise ∀ 3 ↔ ∈ 3 1 ∈ 3 2
extensionality 1 2 3
eliminateForall 1 1
eliminateForall 2 2
eliminateImplication 3 p1
qed

theorem setsWithSameMembershipConditionEqual Sets Are Equal If They Have The Same Membership Condition
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 1
equivalenceIsTransitiveInsideForallRight p1 p2
setsWithSameMembersEqual 1
qed

theorem setsWithEquivalentMembershipConditionsEqual Sets with Equivalent Membership Conditions Are Equal
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 2
premise ∀ 3 ↔ 1 2
equivalenceIsTransitiveInsideForall p1 p3
setsWithSameMembershipConditionEqual 1 p2
qed

theorem equalSetsHaveEquivalentMembershipConditions Equal Sets Have Equivalent Membership Conditions
premise = 1 2
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 2
equalSetsHaveSameMembers p1 3
equivalenceIsTransitiveInsideForallLeft p2 1
equivalenceIsTransitiveInsideForall 2 p3
qed

theorem setWithMembershipConditionIsUnique A Set With a Membership Condition Is Unique
premise ∃ 1 ∀ 2 ↔ ∈ 2 1 1
assume ∧ ∀ 2 ↔ ∈ 2 3 1 ∀ 2 ↔ ∈ 2 4 1
  eliminateAndLeft f.a
  eliminateAndRight f.a
  setsWithSameMembershipConditionEqual f.2 f.1
introduceImplication f.3
uniquenessCondition p1 1
qed

theorem substituteExistenceMembershipCondition Substitute Existence Membership Condition
premise ∃ 1 ∀ 2 ↔ ∈ 2 1 1
premise ∀ 2 ↔ 1 2
assume ∀ 2 ↔ ∈ 2 1' 1
  equivalenceIsTransitiveInsideForall f.a p2
  introduceExists f.1 ∀ 2 ↔ ∈ 2 1 2 1' 1
introduceImplication f.2
eliminateExists p1 1 1'
qed
