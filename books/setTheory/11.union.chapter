chapter Unions
Arbitrary and binary unions of sets.

axiom axiomOfUnion (Axiom of Union) () () (∀ 1 ∃ 2 ∀ 3 ↔ ∈ 3 2 ∃ 4 ∧ ∈ 3 4 ∈ 4 1) () ()

theorem arbitraryUnionsExist Arbitrary Unions Exist
axiomOfUnion 1 2 3 4
eliminateForall 1 1 1
qed

theorem arbitraryUnionsAreUnique Arbitrary Unions Are Unique
arbitraryUnionsExist 2 3 4 1
setWithMembershipConditionIsUnique 1
qed

term union (X) format (⋃X) (∀ y ↔ ∈ y union X ∃ z ∧ ∈ y z ∈ z X)

theorem characteriseElementsOfArbitraryUnion Characterise Elements of Arbitrary Union
definition-union 2 1 3
eliminateForall 1 2 2
qed

theorem characteriseElementOfArbitraryUnion Characterise Element of Arbitrary Union
premise ∈ 2 union 1
characteriseElementsOfArbitraryUnion 2 1 3
modusPonensEquivalenceLeft 1 p1
qed

theorem elementOfElementIsElementOfUnion An Element of an Element of a Set Is an Element of the Union
premise ∈ 1 2
premise ∈ 2 3
introduceAnd p1 p2
introduceExists 1 ∧ ∈ 1 2 ∈ 2 3 2 2
characteriseElementsOfArbitraryUnion 1 3 2
modusPonensEquivalenceRight 3 2
qed

theorem unionOverEmptySetIsEmptySet Union over the Empty Set Is the Empty Set
definition-union 1 ∅ 2
definition-∅ 2
introduceAndInsideNegationRightInsideForall 2 ∈ 1' 2
forallNotIsNotExists 3
introduceForall 4 ¬ ∃ 2 ∧ ∈ 1 2 ∈ 2 ∅ 1' 1
modusTollensEquivalenceInsideForallRight 1 5
setWithNoElementsIsTheEmptySet 6
qed

theorem unionOverSingletonIsSet Union over a Singleton Is the Set
definition-union 1 singleton 2 3
assume ∃ 3 ∧ ∈ 1' 3 ∈ 3 singleton 2
  assume ∧ ∈ 1' 3' ∈ 3' singleton 2
    eliminateAndRight f.f.a
    elementOfSingletonIsSet f.f.1
    eliminateAndLeft f.f.a
    eliminateEquals f.f.2 f.f.3 ∈ 1' 2 2
  introduceImplication f.f.4
  eliminateExists f.a f.1 3'
introduceImplication f.2
assume ∈ 1' 2
  setIsElementOfSingleton 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 1' 3 ∈ 3 singleton 2 2 3
introduceImplication f.3
introduceEquivalence 2 3
introduceForall 4 ↔ ∃ 3 ∧ ∈ 1 3 ∈ 3 singleton 2 ∈ 1 2 1' 1
equivalenceIsTransitiveInsideForall 1 5
setsWithSameMembersEqual 6
qed

theorem unionPowerSetIsSet Union over a Power Set Is the Set
definition-union 1 powerSet 2 3
assume ∃ 3 ∧ ∈ 1' 3 ∈ 3 powerSet 2
  assume ∧ ∈ 1' 3' ∈ 3' powerSet 2
    eliminateAndRight f.f.a
    elementOfPowerSetIsSubset f.f.1 3 2
    unapply-⊆ f.f.2 4
    eliminateForall f.f.3 1'
    eliminateAndLeft f.f.a
    eliminateImplication f.f.4 f.f.5
  introduceImplication f.f.6
  eliminateExists f.a f.1 3'
introduceImplication f.2
assume ∈ 1' 2
  anySetIsAnElementOfItsPowerSet 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 1' 3 ∈ 3 powerSet 2 2 3
introduceImplication f.3
introduceEquivalence 2 3
introduceForall 4 ↔ ∃ 3 ∧ ∈ 1 3 ∈ 3 powerSet 2 ∈ 1 2 1' 1
equivalenceIsTransitiveInsideForall 1 5
setsWithSameMembersEqual 6
qed

theorem unionOverPairMembershipCondition Union over Pair Membership Condition
definition-union 3 pair 1 2 4
assume ∃ 4 ∧ ∈ 3' 4 ∈ 4 pair 1 2
  assume ∧ ∈ 3' 4' ∈ 4' pair 1 2
    eliminateAndLeft f.f.a
    eliminateAndRight f.f.a
    characteriseElementOfPair f.f.2
    assume = 4' 1
      eliminateEquals f.f.f.a f.f.1 ∈ 3' 5 5
    introduceImplication f.f.f.1
    assume = 4' 2
      eliminateEquals f.f.f.a f.f.1 ∈ 3' 5 5
    introduceImplication f.f.f.1
    modusPonensOnOr f.f.3 f.f.4 f.f.5
  introduceImplication f.f.6
  eliminateExists f.a f.1 4'
introduceImplication f.2
assume ∈ 3' 1
  firstElementOfPair 1 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 3' 4 ∈ 4 pair 1 2 1 4
introduceImplication f.3
assume ∈ 3' 2
  secondElementOfPair 2 1
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 3' 4 ∈ 4 pair 1 2 2 4
introduceImplication f.3
combineCases 3 4
introduceEquivalence 2 5
introduceForall 6 ↔ ∃ 4 ∧ ∈ 3 4 ∈ 4 pair 1 2 ∨ ∈ 3 1 ∈ 3 2 3' 3
equivalenceIsTransitiveInsideForall 1 7
qed

term ∪ (X Y) (= ∪ X Y union pair X Y)

theorem binaryUnionMembershipCondition Binary Union Membership Condition
definition-∪ 1 2
unionOverPairMembershipCondition 3 1 2
eliminateEqualsReverse 1 2 ∀ 3 ↔ ∈ 3 4 ∨ ∈ 3 1 ∈ 3 2 4
qed

theorem characteriseElementsOfBinaryUnion Characterise Elements of Binary Union
binaryUnionMembershipCondition 3 1 2
eliminateForall 1 3
qed

theorem unionIsSymmetric Union Is Symmetric
binaryUnionMembershipCondition 3 1 2
orIsSymmetric ∈ 3' 1 ∈ 3' 2
introduceForall 2 ↔ ∨ ∈ 3 1 ∈ 3 2 ∨ ∈ 3 2 ∈ 3 1 3' 3
equivalenceIsTransitiveInsideForall 1 3
binaryUnionMembershipCondition 3 2 1
equivalenceIsTransitiveInsideForallRight 4 5
setsWithSameMembersEqual 6
qed

theorem elementOfUnionLeft Element of Union (Left)
premise ∈ 3 1
introduceOrLeft p1 ∈ 3 2
characteriseElementsOfBinaryUnion 3 1 2
modusPonensEquivalenceRight 2 1
qed


theorem elementOfUnionRight Element of Union (Right)
premise ∈ 3 2
introduceOrRight p1 ∈ 3 1
characteriseElementsOfBinaryUnion 3 1 2
modusPonensEquivalenceRight 2 1
qed
