chapter Empty Set
Introduce the empty set, ∅.

axiom Axiom of Empty Set
conclusion ∃ X ∀ x ¬ ∈ x X
distinct-variables (x X)

theorem Two Empty Sets Are Equal
premise ∀ x ¬ ∈ x X
premise ∀ x ¬ ∈ x Y
prove ∀ x ↔ ∈ x X ∈ x Y
prove = X Y
qed

theorem There Exists a Unique Empty Set
prove ∃ X ∀ x ¬ ∈ x X
assume ∧ ∀ x ¬ ∈ x Y ∀ x ¬ ∈ x Z {
  prove ∀ x ¬ ∈ x Y
  prove ∀ x ¬ ∈ x Z
  prove = Y Z
}
prove ∃! X ∀ x ¬ ∈ x X
qed

term ∅ () (∀ x ¬ ∈ x _)

theorem The Empty Set Has No Elements
prove ∀ x ¬ ∈ x ∅
prove ¬ ∃ x ∈ x ∅
qed

theorem A Set with No Elements Is the Empty Set
premise ∀ y ¬ ∈ y x
prove ∀ y ¬ ∈ y ∅
prove = x ∅
qed

theorem A Set with No Elements Is the Empty Set
premise ¬ ∃ y ∈ y x
prove ∀ y ¬ ∈ y x
prove = x ∅
qed

theorem A Set with an Element Is Not the Empty Set
premise ∃ y ∈ y x
assume = x ∅ {
  prove ∃ y ∈ y ∅
  prove ¬ ∃ y ∈ y ∅
}
prove ¬ = x ∅
qed

theorem A Set with an Element Is Not the Empty Set
premise ∈ y x
prove ∃ y ∈ y x
prove ¬ = x ∅
qed

theorem A Set That Is Not the Empty Set Has an Element
premise ¬ = x ∅
assume ¬ ∃ y ∈ y x {
  prove = x ∅
}
prove ∃ y ∈ y x
qed

theorem The Empty Set Is a Subset of Any Set
prove ∀ x ¬ ∈ x ∅
prove ∀ x → ∈ x ∅ ∈ x X
prove ⊆ ∅ X
qed

theorem A False Statement Is a Membership Condition for the Empty Set
premise ∀ x ¬ φ
prove ∀ x ¬ ∈ x ∅
prove ∀ x ↔ ∈ x ∅ φ
qed
