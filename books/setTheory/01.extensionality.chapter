chapter Set Membership and Extensionality
Introduce the set membership predicate ∈ and the Axiom of Extensionality, and explore their consequences for equality.

statement ∈ (x y)

theorem Equal Sets Have the Same Members
premise = x y
prove ↔ ∈ z x ∈ z y
prove ∀ z ↔ ∈ z x ∈ z y
qed

axiom Axiom of Extensionality
conclusion → ∀ z ↔ ∈ z x ∈ z y = x y
distinct-variables (z x, z y)

theorem Sets with Same Members Are Equal
premise ∀ z ↔ ∈ z x ∈ z y
prove → ∀ z ↔ ∈ z x ∈ z y = x y
prove = x y
qed

theorem Sets with Same Membership Condition Are Equal
premise ∀ z ↔ ∈ z x φ
premise ∀ z ↔ ∈ z y φ
prove ∀ z ↔ ∈ z x ∈ z y
prove = x y
qed

theorem Sets with Equivalent Membership Conditions Are Equal
premise ∀ z ↔ ∈ z x φ
premise ∀ z ↔ ∈ z y ψ
premise ∀ z ↔ φ ψ
prove ∀ z ↔ ∈ z x ψ
prove = x y
qed

theorem A Set with a Membership Condition Is Unique
premise ∃ X ∀ x ↔ ∈ x X φ
assume ∧ ∀ x ↔ ∈ x Y φ ∀ x ↔ ∈ x Z φ {
  prove ∀ x ↔ ∈ x Y φ
  prove ∀ x ↔ ∈ x Z φ
  prove = Y Z
}
prove ∃! X ∀ x ↔ ∈ x X φ
qed

theorem Replace Equivalent Membership Condition
premise ∃ X ∀ x ↔ ∈ x X φ
premise ∀ x ↔ φ ψ
assume ∀ x ↔ ∈ x Y φ {
  prove ∀ x ↔ ∈ x Y ψ
  prove ∃ X ∀ x ↔ ∈ x X ψ
}
prove ∃ X ∀ x ↔ ∈ x X ψ
qed
