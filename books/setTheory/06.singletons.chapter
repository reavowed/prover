chapter Singletons
Establish the existence of the singleton set {a}.

theorem Singleton Existence
prove ∃! x = x a
prove → ∈ y powerSet ∅ ∃! x = x a
prove ∀ y → ∈ y powerSet ∅ ∃! x = x a
prove ∃ b ∀ x ↔ ∈ x b ∃ y ∧ ∈ y powerSet ∅ = x a
prove ∈ ∅ powerSet ∅
prove ∃ y ∈ y powerSet ∅
prove ↔ ∃ y ∧ ∈ y powerSet ∅ = x a = x a
prove ∀ x ↔ ∃ y ∧ ∈ y powerSet ∅ = x a = x a
prove ∃ b ∀ x ↔ ∈ x b = x a
prove ∃! b ∀ x ↔ ∈ x b = x a
qed

term singleton (a)
  name (Singleton)
  format ({a})
  (∀ x ↔ ∈ x _ = x a)
  distinct-variables (x a)

theorem A Set Is an Element of Its Singleton
prove ∀ x ↔ ∈ x singleton a = x a
prove = a a
prove ∈ a singleton a
qed

theorem Element of Singleton
premise ∈ b singleton a
prove ∀ x ↔ ∈ x singleton a = x a
prove = b a
qed

theorem Sets Are Equal If Singletons Are Equal
premise = singleton a singleton b
prove ∀ x ↔ ∈ x singleton a = x a
prove ∀ x ↔ ∈ x singleton b = x a
prove ∀ x ↔ ∈ x singleton b = x b
prove ∀ x ↔ = x a = x b
prove = a b
qed

theorem Singleton Is Subset
premise ∈ a S
prove ∀ x ↔ ∈ x singleton a = x a
assume = x a {
  prove ∈ x S
}
prove → = x a ∈ x S
prove ∀ x → = x a ∈ x S
prove ∀ x → ∈ x singleton a ∈ x S
prove ⊆ singleton a S
qed

theorem Singleton Is in Power Set
premise ∈ a S
prove ⊆ singleton a S
prove ∈ singleton a powerSet S
qed

theorem A Singleton Is Not the Empty Set
prove ∈ a singleton a
prove ¬ = singleton a ∅
qed

theorem Subsets of a Singleton
premise ⊆ S singleton a
prove ∀ x → ∈ x S ∈ x singleton a
prove ∀ x ↔ ∈ x singleton a = x a
prove ∀ x → ∈ x S = x a
assume ∃ y ∈ y S {
  assume ∈ b S {
    prove = b a
    prove ∈ a S
  }
  prove ∈ a S
  prove ⊆ singleton a S
  prove = S singleton a
}
assume ¬ ∃ y ∈ y S {
  prove = S ∅
}
prove ∨ = S singleton a = S ∅
prove ¬ = singleton a ∅
prove ⊕ = S singleton a = S ∅
qed

theorem Elements of Powerset of Singleton
premise ∈ S powerSet singleton a
prove ⊆ S singleton a
prove ⊕ = S singleton a = S ∅
qed

