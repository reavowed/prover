chapter Singletons
Establish the existence of the singleton set {x}.

theorem Singletons Exist
prove ∃! Y = Y X
prove → ∈ x powerSet ∅ ∃! Y = Y X
prove ∀ x → ∈ x powerSet ∅ ∃! Y = Y X
prove ∃ Z ∀ Y ↔ ∈ Y Z ∃ x ∧ ∈ x powerSet ∅ = Y X
prove ∈ ∅ powerSet ∅
prove ∃ x ∈ x powerSet ∅
prove ↔ ∃ x ∧ ∈ x powerSet ∅ = Y X = Y X
prove ∀ Y ↔ ∃ x ∧ ∈ x powerSet ∅ = Y X = Y X
prove ∃ Z ∀ Y ↔ ∈ Y Z = Y X
qed

theorem Singletons Are Unique
prove ∃ Z ∀ Y ↔ ∈ Y Z = Y X
prove ∃! Z ∀ Y ↔ ∈ Y Z = Y X
qed

term singleton (x)
  name (Singleton)
  format ({x})
  (∀ y ↔ ∈ y _ = y x)

theorem A Set Is an Element of Its Singleton
prove ∀ y ↔ ∈ y singleton x = y x
prove = x x
prove ∈ x singleton x
qed

theorem Element of Singleton
premise ∈ y singleton x
prove ∀ y ↔ ∈ y singleton x = y x
prove = y x
qed

theorem Sets Are Equal If Singletons Are Equal
premise = singleton x singleton y
prove ∀ z ↔ ∈ z singleton x = z x
prove ∀ z ↔ ∈ z singleton y = z x
prove ∀ z ↔ ∈ z singleton y = z y
prove ∀ z ↔ = z x = z y
prove = x y
qed

theorem Singleton Is Subset
premise ∈ x X
prove ∀ y ↔ ∈ y singleton x = y x
assume = y x {
  prove ∈ y X
}
prove → = y x ∈ y X
prove ∀ y → = y x ∈ y X
prove ∀ y → ∈ y singleton x ∈ y X
prove ⊆ singleton x X
qed

theorem Singleton Is in Power Set
premise ∈ x X
prove ⊆ singleton x X
prove ∈ singleton x powerSet X
qed

theorem A Singleton Is Not the Empty Set
prove ∈ x singleton x
prove ¬ = singleton x ∅
qed

theorem Subsets of a Singleton
premise ⊆ y singleton x
prove ∀ z → ∈ z y ∈ z singleton x
prove ∀ z ↔ ∈ z singleton x = z x
prove ∀ z → ∈ z y = z x
assume ∃ z ∈ z y {
  assume ∈ z y {
    prove = z x
    prove ∈ x y
  }
  prove ∈ x y
  prove ⊆ singleton x y
  prove = y singleton x
}
assume ¬ ∃ z ∈ z y {
  prove = y ∅
}
prove ∨ = y singleton x = y ∅
prove ¬ = singleton x ∅
prove ⊕ = y singleton x = y ∅
qed

theorem Elements of Powerset of Singleton
premise ∈ x powerSet singleton ∅
prove ⊆ x singleton ∅
prove ⊕ = x singleton ∅ = x ∅
qed

