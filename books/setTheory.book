book Set Theory
import Predicate Calculus

chapter Set Membership
Introduce the set membership relation, ∈.

predicate ∈ 2

theorem substituteEqualsIntoElement Substitute equal terms into set element
premise = 1 2
assume ∈ 1 3
  eliminateEquals p1 f.a ∈ 4 3 4
introduceImplication f.1
reverseEquality p1
assume ∈ 2 3
  eliminateEquals 2 f.a ∈ 4 3 4
introduceImplication f.1
introduceEquivalence 1 3
qed

theorem substituteEqualsIntoContainer Substitute equal terms into set container
premise = 1 2
assume ∈ 3 1
  eliminateEquals p1 f.a ∈ 3 4 4
introduceImplication f.1
reverseEquality p1
assume ∈ 3 2
  eliminateEquals 2 f.a ∈ 3 4 4
introduceImplication f.1
introduceEquivalence 1 3
qed

theorem equalSetsHaveSameElements Equal Sets Have the Same Elements
premise = 1 2
assume ∈ 3' 1
  eliminateEquals p1 f.a ∈ 3' 4 4
introduceImplication f.1
assume ∈ 3' 2
  eliminateEqualsReverse p1 f.a ∈ 3' 4 4
introduceImplication f.1
introduceEquivalence 1 2
introduceForall 3 ↔ ∈ 3 1 ∈ 3 2 3' 3
qed

theorem setsWithDifferentElementsNotEqual1 Sets with Different Elements Are Not Equal (1)
premise ∈ 3 1
premise ¬ ∈ 3 2
assume = 1 2
  eliminateEquals f.a p1 ∈ 3 4 4
introduceNegation f.1 p2
qed

theorem setsWithDifferentElementsNotEqual2 Sets with Different Elements Are Not Equal (2)
premise ∈ 3 1
premise ¬ ∈ 3 2
assume = 2 1
  eliminateEquals f.a p2 ¬ ∈ 3 4 4
introduceNegation p1 f.1
qed

chapter Extensionality
Introduce the Axiom of Extensionality and explore its consequences for equality.

axiom extensionality Extensionality ∀ 1 ∀ 2 → ∀ 3 ↔ ∈ 3 1 ∈ 3 2 = 1 2

theorem setsWithSameMembersEqual Sets Are Equal If They Have The Same Members
premise ∀ 3 ↔ ∈ 3 1 ∈ 3 2
extensionality 1 2 3
eliminateForall 1 1
eliminateForall 2 2
eliminateImplication 3 p1
qed

theorem setsWithSameMembershipConditionEqual Sets Are Equal If They Have The Same Membership Condition
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 1
eliminateForall p1 4
eliminateForall p2 4
equivalenceIsTransitiveRight 1 2
introduceForall 3 ↔ ∈ 3 1 ∈ 3 2 4 3
setsWithSameMembersEqual 4
qed

theorem setsWithEquivalentMembershipConditionsEqual Sets with Equivalent Membership Conditions Are Equal
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 2
premise ∀ 3 ↔ 1 2
equivalenceIsTransitiveInsideForall p1 p3
setsWithSameMembershipConditionEqual 1 p2
qed

theorem equalSetsHaveEquivalentMembershipConditions Equal Sets Have Equivalent Membership Conditions
premise = 1 2
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 2
equalSetsHaveSameElements p1 3
equivalenceIsTransitiveInsideForallLeft p2 1
equivalenceIsTransitiveInsideForall 2 p3
qed

theorem setIsEqualIffItHasSameMembershipCondition A Set Is Equal to Another Iff It Has the Same Membership Condition
premise ∀ 3 ↔ ∈ 3 1 1
assume ∀ 3 ↔ ∈ 3 2 1
  setsWithSameMembershipConditionEqual f.a p1
introduceImplication f.1
assume = 2 1
  eliminateEqualsReverse f.a p1 ∀ 3 ↔ ∈ 3 4 1 4
introduceImplication f.1
introduceEquivalence 1 2
qed

theorem setWithMembershipConditionIsUnique A Set With a Membership Condition Is Unique
premise ∃ 1 ∀ 2 ↔ ∈ 2 1 1
assume ∀ 2 ↔ ∈ 2 3 1
  setIsEqualIffItHasSameMembershipCondition f.a 3'
  introduceForall f.1 ↔ ∀ 2 ↔ ∈ 2 1 1 = 1 3 3' 1
  introduceExists f.2 ∀ 1 ↔ ∀ 2 ↔ ∈ 2 1 1 = 1 4 3 4
  apply-∃! f.3
introduceImplication f.4
eliminateExists p1 1 3
qed

theorem equivalenceOfMembershipConditions Equivalence of Membership Conditions
premise ∃ 1 ∀ 2 ↔ ∈ 2 1 1
premise ∀ 2 ↔ 1 2
assume ∀ 2 ↔ ∈ 2 1' 1
  eliminateForall f.a 3
  eliminateForall p2 3
  equivalenceIsTransitive f.1 f.2
  introduceForall f.3 ↔ ∈ 2 1' 2 3 2
  introduceExists f.4 ∀ 2 ↔ ∈ 2 1 2 1' 1
introduceImplication f.5
eliminateExists p1 1 1'
qed

chapter Subsets
Introduce the subset relation, ⊆.

predicate ⊆ 2 ∀ 3 → ∈ 3 1 ∈ 3 2

theorem subsetIsReflexive Subset is Reflexive
implicationIsReflexive ∈ 3 1
introduceForall 1 → ∈ 2 1 ∈ 2 1 3 2
apply-⊆ 2
qed

theorem subsetIsAntisymmetric Subset is Antisymmetric
premise ⊆ 1 2
premise ⊆ 2 1
unapply-⊆ p1 3
unapply-⊆ p2 3
introduceEquivalenceInsideForall 1 2
setsWithSameMembersEqual 3
qed

theorem subsetIsTransitive Subset is Transitive
premise ⊆ 1 2
premise ⊆ 2 3
unapply-⊆ p1 3
unapply-⊆ p2 3
implicationIsTransitiveInsideForall 1 2
apply-⊆ 3
qed

theorem equalsImpliesSubset Equals Implies Subset
premise = 1 2
subsetIsReflexive 1
eliminateEquals p1 1 ⊆ 1 3 3
qed

chapter Empty Set
Introduce the empty set, ∅.
axiom emptySetExists "Empty Set" ∃ 1 ∀ 2 ¬ ∈ 2 1

theorem emptySetsAreEqual Empty Sets are Equal
premise ∀ 3 ¬ ∈ 3 1
premise ∀ 3 ¬ ∈ 3 2
eliminateForall p1 4 3
eliminateForall p2 4 3
falseStatementsAreEquivalent 1 2
introduceForall 3 ↔ ∈ 3 1 ∈ 3 2 4 3
setsWithSameMembersEqual 4
qed

theorem thereIsAUniqueEmptySet There Is a Unique Empty Set
emptySetExists 1 2
assume ∀ 2 ¬ ∈ 2 3
  assume ∀ 2 ¬ ∈ 2 4
    emptySetsAreEqual f.f.a f.a
  introduceImplication f.f.1
  assume = 4 3
    substituteEqualsIntoContainer f.f.a 5 4
    negateEquivalence f.f.1
    eliminateForall f.a 5 3
    modusPonensEquivalenceRight f.f.2 f.f.3
    introduceForall f.f.4 ¬ ∈ 2 4 5 2
  introduceImplication f.f.5
  introduceEquivalence f.1 f.2
  introduceForall f.3 ↔ ∀ 2 ¬ ∈ 2 1 = 1 3 4 1
  introduceExists f.4 ∀ 1 ↔ ∀ 2 ¬ ∈ 2 1 = 1 3 3 3
  apply-∃! f.5
introduceImplication f.6
eliminateExists 1 2 3
qed

term ∅ () () () (∀ 1 ¬ ∈ 1 ∅)

theorem anEmptySetIsTheEmptySet An Empty Set Is The Empty Set
premise ∀ 2 ¬ ∈ 2 1
definition-∅ 2
emptySetsAreEqual p1 1
qed

theorem setIsNotInEmptySet A Set Is Not in the Empty Set
definition-∅ 1
eliminateForall 1 1
qed

theorem theEmptySetIsASubsetOfAnySet The Empty Set Is a Subset of Any Set
definition-∅ 1
eliminateForall 1 2 1
falseImpliesAnything1 2 ∈ 2 1
introduceForall 3 → ∈ 3 ∅ ∈ 3 1 2 3
apply-⊆ 4
qed

theorem theOnlySubsetOfTheEmptySetIsTheEmptySet The Only Subset of the Empty Set is the Empty Set
assume ⊆ 1 ∅
  unapply-⊆ f.a 2
  definition-∅ 2
  modusTollensInsideForall f.1 f.2
  emptySetsAreEqual f.3 f.2
introduceImplication f.4
introduceImplication equalsImpliesSubset = 1 ∅
introduceEquivalence 1 2
qed

theorem falseStatementCharacterisesTheEmptySet A False Statement Characterises The Empty Set
premise ∀ 1 ¬ 1
definition-∅ 1
falseStatementsAreEquivalentInsideForall 1 p1
qed

theorem nonEmptySetHasAnElement A Non-Empty Set Has an Element
premise ¬ = 1 ∅
assume ¬ ∃ 2 ∈ 2 1
  notExistsIsForallNot f.a
  anEmptySetIsTheEmptySet f.1
introduceNegation f.2 p1
eliminateNegation 1
qed

theorem setWithSomeElementIsNonEmpty A Set with Some Element Is Non-Empty
premise ∃ 2 ∈ 2 1
unapply-∃ p1
assume = 1 ∅
  definition-∅ 2
  eliminateEqualsReverse f.a f.1 ∀ 2 ¬ ∈ 2 1 1
introduceNegation f.2 1
qed

theorem setWithAnElementIsNonEmpty A Set with an Element Is Non-Empty
premise ∈ 2 1
introduceExists p1 ∈ 2 1 2 2
setWithSomeElementIsNonEmpty 1
qed

chapter Power Sets
Define the power set of a set - the set of all its subsets.

axiom axiomOfPowerset "Power Set" ∀ 1 ∃ 2 ∀ 3 ↔ ∈ 3 2 ⊆ 3 1

term powerSet (term) (𝒫{}) () (∀ 2 ↔ ∈ 2 powerSet 1 ⊆ 2 1)

theorem elementOfPowersetIsSubset An Element of a Power Set is a Subset
premise ∈ 2 powerSet 1
definition-powerSet 2 1
eliminateForall 1 2 2
modusPonensEquivalenceLeft 2 p1
qed

theorem subsetIsElementOfPowerset A Subset is An Element of a Power Set
premise ⊆ 2 1
definition-powerSet 2 1
eliminateForall 1 2 2
modusPonensEquivalenceRight 2 p1
qed

theorem theEmptySetIsAnElementOfAnyPowerSet The Empty Set Is an Element of Any Power Set
theEmptySetIsASubsetOfAnySet 1
subsetIsElementOfPowerset 1
qed

theorem anySetIsAnElementOfItsPowerSet Any Set Is an Element of Its Power Set
subsetIsReflexive 1
subsetIsElementOfPowerset 1
qed

theorem powerSetOfEmptySetContainsExactlyEmptySet The Power Set of The Empty Set Contains Exactly the Empty Set
definition-powerSet 1 ∅
eliminateForall 1 1 1
theOnlySubsetOfTheEmptySetIsTheEmptySet 1
equivalenceIsTransitive 2 3
qed

chapter Replacement
Introduce the Axiom of Replacement.

axiom axiomOfReplacement Replacement → ∀ 3 → ∈ 3 1 ∃! 4 1 ∃ 2 ∀ 4 ↔ ∈ 4 2 ∃ 3 ∧ ∈ 3 1 1

theorem replacementInference Replacement Inference
premise ∀ 3 → ∈ 3 1 ∃! 4 1
axiomOfReplacement 1 3 1 4 2
eliminateImplication 1 p1
qed

chapter Singletons
Establish the existence of the singleton set {x}.

theorem singletonsExist Singletons Exist
thereExistsAUniqueSetEqualToAnySet 3 1
anythingImpliesTrue 1 ∈ 4' powerSet ∅
introduceForall 2 → ∈ 4 powerSet ∅ ∃! 3 = 3 1 4' 4
replacementInference 3 2
assume ∃ 4 ∧ ∈ 4 powerSet ∅ = 3' 1
  introduceImplication eliminateAndRight ∧ ∈ 4' powerSet ∅ = 3' 1
  eliminateExists f.a f.1 4'
introduceImplication f.2
assume = 3' 1
  theEmptySetIsAnElementOfAnyPowerSet ∅
  introduceAnd f.1 f.a
  introduceExists f.2 ∧ ∈ 4 powerSet ∅ = 3' 1 ∅ 4
introduceImplication f.3
introduceEquivalence 5 6
assume ∀ 3 ↔ ∈ 3 2' ∃ 4 ∧ ∈ 4 powerSet ∅ = 3 1
  eliminateForall f.a 3'
  equivalenceIsTransitive f.1 7
  introduceForall f.2 ↔ ∈ 3 2' = 3 1 3' 3
  introduceExists f.3 ∀ 3 ↔ ∈ 3 2 = 3 1 2' 2
introduceImplication f.4
eliminateExists 4 8 2'
qed

theorem singletonsAreUnique Singletons are Unique
singletonsExist 2 3 1
setWithMembershipConditionIsUnique 1
qed

term singleton (term) ({{}}) () (∀ 2 ↔ ∈ 2 singleton 1 = 2 1)

theorem characteriseElementsOfSingleton Characterise Elements of a Singleton
definition-singleton 2 1
eliminateForall 1 2
qed

theorem setIsElementOfSingleton A Set is an Element of Its Singleton
characteriseElementsOfSingleton 1 1
equalityIsReflexive 1
modusPonensEquivalenceRight 1 2
qed

theorem elementOfSingletonIsSet An Element of a Singleton is the Set Itself
premise ∈ 2 singleton 1
characteriseElementsOfSingleton 2 1
modusPonensEquivalenceLeft 1 p1
qed

theorem singletonIsNonEmpty A Singleton Is Non-Empty
setIsElementOfSingleton 1
introduceExists 1 ∈ 2 singleton 1 1 2
setWithSomeElementIsNonEmpty 2
qed

theorem emptySetNotEqualToItsSingleton The Empty Set Is Not Equal To Its Singleton
setIsElementOfSingleton ∅
definition-∅ 1
eliminateForall 2 ∅
setsWithDifferentElementsNotEqual2 1 3
qed

theorem singletonIsSubsetIfSetIsElement Singleton Is a Subset If the Set Is an Element
premise ∈ 2 1
assume ∈ 3' singleton 2
  elementOfSingletonIsSet f.a
  eliminateEqualsReverse f.1 p1 ∈ 4 1 4
introduceImplication f.2
introduceForall 1 → ∈ 3 singleton 2 ∈ 3 1 3' 3
apply-⊆ 2
qed

theorem setsAreEqualIfTheirSingletonsAreEqual Sets Are Equal If Their Singletons Are Equal
premise = singleton 1 singleton 2
setIsElementOfSingleton 1
eliminateEquals p1 1 ∈ 1 3 3
elementOfSingletonIsSet 2
qed

theorem subsetsOfSingleton Subsets of a Singleton
assume ∈ 2 powerSet singleton 1
  elementOfPowersetIsSubset f.a
  unapply-⊆ f.1 3
  definition-singleton 3 1
  implicationThenEquivalenceInsideForall f.2 f.3
  assume ∃ 3 ∈ 3 2
    assume ∈ 3' 2
      modusPonensInsideForall f.4 f.f.f.a 3'
      eliminateEquals f.f.f.1 f.f.f.a ∈ 4 2 4
    introduceImplication f.f.f.2
    eliminateExists f.f.a f.f.1 3'
    singletonIsSubsetIfSetIsElement f.f.2
    subsetIsAntisymmetric f.1 f.f.3
    introduceOrRight f.f.4 = 2 ∅
  introduceImplication f.f.5
  assume ¬ ∃ 3 ∈ 3 2
    notExistsIsForallNot f.f.a
    anEmptySetIsTheEmptySet f.f.1
    introduceOrLeft f.f.2 = 2 singleton 1
  introduceImplication f.f.3
  proofByCases f.5 f.6
introduceImplication f.7
assume = 2 ∅
  theEmptySetIsAnElementOfAnyPowerSet singleton 1
  eliminateEqualsReverse f.a f.1 ∈ 3 powerSet singleton 1 3
introduceImplication f.2
assume = 2 singleton 1
  anySetIsAnElementOfItsPowerSet singleton 1
  eliminateEqualsReverse f.a f.1 ∈ 3 powerSet singleton 1 3
introduceImplication f.2
combineCases 2 3
introduceEquivalence 1 4
qed

chapter Unordered Pairs
Establish the existence of the unordered pair {x,y}.

theorem unorderedPairExistenceLemma1 Unordered Pair Existence Lemma 1
premise ∨ 1 2
premise ¬ ∧ 1 2
assume 1
  eliminateNegatedConjunctRight f.a p2
  disjoinedConjunctEquivalenceLeft1 f.a f.1 = 3' 1 = 3' 2
  introduceForall f.2 ↔ ∨ ∧ 1 = 3 1 ∧ 2 = 3 2 = 3 1 3' 3
  introduceExists f.3 ∀ 3 ↔ ∨ ∧ 1 = 3 1 ∧ 2 = 3 2 = 3 4 1 4
  apply-∃! f.4
introduceImplication f.5
assume 2
  eliminateNegatedConjunctLeft f.a p2
  orIsSymmetric ∧ 1 = 3' 1 ∧ 2 = 3' 2
  disjoinedConjunctEquivalenceLeft1 f.a f.1 = 3' 2 = 3' 1
  equivalenceIsTransitive f.2 f.3
  introduceForall f.4 ↔ ∨ ∧ 1 = 3 1 ∧ 2 = 3 2 = 3 2 3' 3
  introduceExists f.5 ∀ 3 ↔ ∨ ∧ 1 = 3 1 ∧ 2 = 3 2 = 3 4 2 4
  apply-∃! f.6
introduceImplication f.7
eliminateOr p1 1 2
qed

theorem unorderedPairExistenceLemma2 Unordered Pair Existence Lemma 2
assume ∈ 3' powerSet singleton ∅
  subsetsOfSingleton 3' ∅
  modusPonensEquivalenceLeft f.1 f.a
  assume ∧ = 3' ∅ = 3' singleton ∅
    eliminateAndLeft f.f.a
    eliminateAndRight f.f.a
    equalityIsTransitiveLeft f.f.2 f.f.1
    singletonIsNonEmpty ∅
  introduceNegation f.f.3 f.f.4
  unorderedPairExistenceLemma1 f.2 f.3 4 1 2
introduceImplication f.4
introduceForall 1 → ∈ 3 powerSet singleton ∅ ∃! 4 ∨ ∧ = 3 ∅ = 4 1 ∧ = 3 singleton ∅ = 4 2 3' 3
qed

theorem unorderedPairExistenceLemma3 Unordered Pair Existence Lemma 3
assume ∃ 3 ∧ ∈ 3 powerSet singleton ∅ ∨ ∧ = 3 ∅ = 4' 1 ∧ = 3 singleton ∅ = 4' 2
  assume ∧ ∈ 3' powerSet singleton ∅ ∨ ∧ = 3' ∅ = 4' 1 ∧ = 3' singleton ∅ = 4' 2
    eliminateAndRight f.f.a
    assume ∧ = 3' ∅ = 4' 1
      eliminateAndRight f.f.f.a
      introduceOrLeft f.f.f.1 = 4' 2
    introduceImplication f.f.f.2
    assume ∧ = 3' singleton ∅ = 4' 2
      eliminateAndRight f.f.f.a
      introduceOrRight f.f.f.1 = 4' 1
    introduceImplication f.f.f.2
    eliminateOr f.f.1 f.f.2 f.f.3
  introduceImplication f.f.4
  eliminateExists f.a f.1 3'
introduceImplication f.2
assume = 4' 1
  equalityIsReflexive ∅
  introduceAnd f.1 f.a
  introduceOrLeft f.2 ∧ = ∅ singleton ∅ = 4' 2
  theEmptySetIsAnElementOfAnyPowerSet singleton ∅
  introduceAnd f.4 f.3
  introduceExists f.5 ∧ ∈ 3 powerSet singleton ∅ ∨ ∧ = 3 ∅ = 4' 1 ∧ = 3 singleton ∅ = 4' 2 ∅ 3
introduceImplication f.6
assume = 4' 2
  equalityIsReflexive singleton ∅
  introduceAnd f.1 f.a
  introduceOrRight f.2 ∧ = singleton ∅ ∅ = 4' 1
  anySetIsAnElementOfItsPowerSet singleton ∅
  introduceAnd f.4 f.3
  introduceExists f.5 ∧ ∈ 3 powerSet singleton ∅ ∨ ∧ = 3 ∅ = 4' 1 ∧ = 3 singleton ∅ = 4' 2 singleton ∅ 3
introduceImplication f.6
combineCases 2 3
introduceEquivalence 1 4
introduceForall 5 ↔ ∃ 3 ∧ ∈ 3 powerSet singleton ∅ ∨ ∧ = 3 ∅ = 4 1 ∧ = 3 singleton ∅ = 4 2 ∨ = 4 1 = 4 2 4' 4
qed

theorem unorderedPairsExist Unordered Pairs Exist
unorderedPairExistenceLemma2 3 4 1 2
replacementInference 1 5
unorderedPairExistenceLemma3 4 3 1 2
equivalenceOfMembershipConditions 2 3
qed

theorem unorderedPairsUnique Unordered Pairs Are Unique
unorderedPairsExist 5 4 1 2
setWithMembershipConditionIsUnique 1
qed

term pair (term term) ({{},{}}) () (∀ 3 ↔ ∈ 3 pair 1 2 ∨ = 3 1 = 3 2)

theorem characteriseElementsOfPair Characterise Elements of Pair
definition-pair 3 1 2
eliminateForall 1 3
qed

theorem characteriseElementOfPair Characterise Element of Pair
premise ∈ 3 pair 1 2
characteriseElementsOfPair 3 1 2
modusPonensEquivalenceLeft 1 p1
qed

theorem firstElementOfPair First Element of Pair
characteriseElementsOfPair 1 1 2
equalityIsReflexive 1
introduceOrLeft 2 = 1 2
modusPonensEquivalenceRight 1 3
qed

theorem secondElementOfPair Second Element of Pair
characteriseElementsOfPair 2 1 2
equalityIsReflexive 2
introduceOrRight 2 = 2 1
modusPonensEquivalenceRight 1 3
qed

theorem pairIsSymmetric Pair is Symmetric
definition-pair 3 1 2
definition-pair 3 2 1
orIsSymmetric = 3' 1 = 3' 2
introduceForall 3 ↔ ∨ = 3 1 = 3 2 ∨ = 3 2 = 3 1 3' 3
setsWithEquivalentMembershipConditionsEqual 1 2 4
qed

theorem repeatedPairIsSingleton A Repeated Pair Is a Singleton
definition-pair 3 1 1
definition-singleton 3 1
orIsIdempotent = 3' 1
reverseEquivalence 3
introduceForall 4 ↔ ∨ = 3 1 = 3 1 = 3 1 3' 3
setsWithEquivalentMembershipConditionsEqual 1 2 5
qed

theorem setIsFirstElementIfSingletonIsPair Set Is First Element If Singleton Is Pair
premise = singleton 1 pair 2 3
definition-singleton 4 1
definition-pair 4 2 3
equalSetsHaveEquivalentMembershipConditions p1 1 2
eliminateForall 3 2
equalityIsReflexive 2
modusPonensEquivalenceRightOrLeft 4 5
reverseEquality 6
qed

theorem setIsSecondElementIfSingletonIsPair Set Is Second Element If Singleton Is Pair
premise = singleton 1 pair 2 3
definition-singleton 4 1
definition-pair 4 2 3
equalSetsHaveEquivalentMembershipConditions p1 1 2
eliminateForall 3 3
equalityIsReflexive 3
modusPonensEquivalenceRightOrRight 4 5
reverseEquality 6
qed

theorem elementsAreEqualIfSingletonIsPair Elements Are Equal If Singleton Is Pair
premise = singleton 1 pair 2 3
setIsFirstElementIfSingletonIsPair p1
setIsSecondElementIfSingletonIsPair p1
equalityIsTransitiveLeft 1 2
qed

chapter Ordered Pairs
Establish the existence of the ordered pair (x,y).

term orderedPair (term term) (({},{})) () (= orderedPair 1 2 pair singleton 1 pair 1 2)

theorem characteriseElementsOfOrderedPair Characterise Elements of Ordered Pair
definition-orderedPair 1 2
definition-pair 3 singleton 1 pair 1 2
eliminateEqualsReverse 1 2 ∀ 3 ↔ ∈ 3 4 ∨ = 3 singleton 1 = 3 pair 1 2 4
eliminateForall 3 3
qed

theorem characteriseElementOfOrderedPair Characterise Element of Ordered Pair
premise ∈ 3 orderedPair 1 2
characteriseElementsOfOrderedPair 3 1 2
modusPonensEquivalenceLeft 1 p1
qed

theorem singletonFirstElementIsElementOfOrderedPair Singleton of First Element is Element of Ordered Pair
definition-orderedPair 1 2
firstElementOfPair singleton 1 pair 1 2
eliminateEqualsReverse 1 2 ∈ singleton 1 2 2
qed

theorem pairIsElementOfOrderedPair Pair Is Element of Ordered Pair
definition-orderedPair 1 2
secondElementOfPair pair 1 2 singleton 1
eliminateEqualsReverse 1 2 ∈ pair 1 2 3 3
qed

theorem elementsOfOrderedPairEqualIfPairIsSingleton Elements of An Ordered Pair Are Equal If The Pair Is A Singleton
premise = orderedPair 1 2 singleton 3
singletonFirstElementIsElementOfOrderedPair 1 2
eliminateEquals p1 1 ∈ singleton 1 3 3
elementOfSingletonIsSet 2
pairIsElementOfOrderedPair 1 2
eliminateEquals p1 4 ∈ pair 1 2 3 3
elementOfSingletonIsSet 5
equalityIsTransitiveRight 3 6
elementsAreEqualIfSingletonIsPair 7
qed

theorem repeatedOrderedPairIsDoubleSingleton A Repeated Ordered Pair Is a Double Singleton
definition-orderedPair 1 1
repeatedPairIsSingleton 1
eliminateEquals 2 1 = orderedPair 1 1 pair singleton 1 2 2
repeatedPairIsSingleton singleton 1
equalityIsTransitive 3 4
qed

theorem equalOrderedPairsHaveEqualFirstElements Equal Ordered Pairs Have Equal First Elements
premise = orderedPair 1 2 orderedPair 3 4
singletonFirstElementIsElementOfOrderedPair 1 2
eliminateEquals p1 1 ∈ singleton 1 3 3
characteriseElementOfOrderedPair 2
introduceImplication setsAreEqualIfTheirSingletonsAreEqual = singleton 1 singleton 3
introduceImplication setIsFirstElementIfSingletonIsPair = singleton 1 pair 3 4
eliminateOr 3 4 5
qed

theorem equalOrderedPairsHaveEqualSecondElements Equal Ordered Pairs Have Equal Second Elements
premise = orderedPair 1 2 orderedPair 3 4
equalOrderedPairsHaveEqualFirstElements p1
eliminateEqualsReverse 1 p1 = orderedPair 1 2 orderedPair 5 4 5
pairIsElementOfOrderedPair 1 2
eliminateEquals 2 3 ∈ pair 1 2 3 3
characteriseElementOfOrderedPair 4
assume = 1 2
  eliminateEqualsReverse f.a 2 = orderedPair 1 5 orderedPair 1 4 5
  repeatedOrderedPairIsDoubleSingleton 1
  equalityIsTransitiveLeft f.1 f.2
  elementsOfOrderedPairEqualIfPairIsSingleton f.3
  equalityIsTransitiveLeft f.a f.4
introduceImplication f.5
assume = pair 1 2 singleton 1
  reverseEquality f.a
  elementsAreEqualIfSingletonIsPair f.1
  eliminateImplication 6 f.2
introduceImplication f.3
assume = pair 1 2 pair 1 4
  secondElementOfPair 2 1
  eliminateEquals f.a f.1 ∈ 2 1 1
  characteriseElementOfPair f.2
  introduceImplication reverseEquality = 2 1
  implicationIsTransitive f.4 6
  collapseOr f.3 f.5
introduceImplication f.6
eliminateOr 5 7 8
qed

chapter Set Comprehension
Introduce set comprehension, {z∈y|α}.

theorem setComprehensionLemma1 Set Comprehension Lemma 1
assume 1
  disjoinedConjunctEquivalenceLeft2 f.a f.a = 3' 1 = 3' 2
  introduceUniqueness f.1 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 2 3
introduceImplication f.2
assume ¬ 1
  disjoinedConjunctEquivalenceRight1 f.a f.a = 3' 1 = 3' 2
  introduceUniqueness f.1 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 2 3
introduceImplication f.2
proofByCases 1 2
qed

theorem setComprehensionLemma2 Set Comprehension Lemma 2
setComprehensionLemma1 sub 1' 1 1 3 1' 4
anythingImpliesTrue 1 ∈ 1' 2
introduceForall 2 → ∈ 1 2 ∃! 3 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 4 1' 1
qed

theorem setComprehensionLemma3 Set Comprehension Lemma 3
premise ∧ ∈ 5 2 sub 5 1 1
assume ∧ ∈ 1' 2 ∨ ∧ sub 1' 1 1 = 3 1' ∧ ¬ sub 1' 1 1 = 3 5
  eliminateAndLeft f.a
  eliminateAndRight f.a
  assume sub 1' 1 1
    pickDisjoinedConjunctLeft f.2 f.f.a f.f.a
    introduceAnd f.1 f.f.a
    eliminateEqualsReverse f.f.1 f.f.2 ∧ ∈ 3 2 sub 3 1 1 3
  introduceImplication f.f.3
  assume ¬ sub 1' 1 1
    pickDisjoinedConjunctRight f.2 f.f.a f.f.a
    eliminateEqualsReverse f.f.1 p1 ∧ ∈ 4 2 sub 4 1 1 4
  introduceImplication f.f.2
  proofByCases f.3 f.4
introduceImplication f.5
introduceExistsInAntecedent 1 ∧ ∈ 1 2 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 5 1' 1
assume ∧ ∈ 3 2 sub 3 1 1
  equalityIsReflexive 3
  rearrangeAndWithOr f.a f.1 ∧ ¬ sub 3 1 1 = 3 5
  introduceExists f.2 ∧ ∈ 1 2 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 5 3 1
introduceImplication f.3
introduceEquivalence 2 3
qed

theorem setComprehensionLemma4 Set Comprehension Lemma 4
premise ∧ ∈ 5 2 sub 5 1 1
setComprehensionLemma2 1 1 2 3 5
replacementInference 1 4
assume ∀ 3 ↔ ∈ 3 4' ∃ 1 ∧ ∈ 1 2 ∨ ∧ 1 = 3 1 ∧ ¬ 1 = 3 5 3 1
  eliminateForall f.a 3'
  setComprehensionLemma3 p1 3'
  equivalenceIsTransitive f.1 f.2
  introduceForall f.3 ↔ ∈ 1 4' ∧ ∈ 1 2 1 3' 1
introduceImplication f.4
eliminateExists 2 3 4'
introduceExists 4 ∀ 1 ↔ ∈ 1 4 ∧ ∈ 1 2 1 4' 4
qed

theorem setComprehensionLemma5 Set Comprehension Lemma 5
premise ∃ 3 ∧ ∈ 3 2 1
assume ∧ ∈ 3' 2 sub 3' 3 1
  setComprehensionLemma4 f.a 4
introduceImplication f.1
eliminateExists p1 1 3'
qed

theorem setComprehensionLemma6 Set Comprehension Lemma 6
premise ¬ ∃ 3 ∧ ∈ 3 2 1
notExistsIsForallNot p1
falseStatementCharacterisesTheEmptySet 1 5
introduceExists 2 ∀ 3 ↔ ∈ 3 4 ∧ ∈ 3 2 1 ∅ 4
qed

theorem setComprehensionsExist Set Comprehensions Exist
introduceImplication setComprehensionLemma5 ∃ 3 ∧ ∈ 3 2 1 4
introduceImplication setComprehensionLemma6 ¬ ∃ 3 ∧ ∈ 3 2 1 4
proofByCases 1 2
qed

theorem setComprehensionIsUnique Set Comprehension Is Unique
setComprehensionsExist sub 3 1 1 4 3 2
setWithMembershipConditionIsUnique 1
qed

term comprehension (term term statement) ({{} ∈ {} | {}}) () (∀ 3 ↔ ∈ 3 comprehension 1 2 1 ∧ ∈ 3 2 sub 3 1 1)

theorem characteriseElementsOfSetComprehension Characterise Elements of Set Comprehension
definition-comprehension 1 3 1 2
eliminateForall 1 3
qed

theorem characteriseElementOfSetComprehension Characterise Element of Set Comprehension
premise ∈ 3 comprehension 1 2 1
characteriseElementsOfSetComprehension 1 3 1 2
modusPonensEquivalenceLeft 1 p1
qed

theorem comprehensionOverEmptySetIsEmpty A Comprehension Over the Empty Set is Empty
definition-comprehension 1 2 1 ∅
definition-∅ 2
introduceAndInsideNegationLeftInsideForall 2 sub 2 1 1
modusTollensEquivalenceInsideForallRight 1 3
anEmptySetIsTheEmptySet 4
qed

chapter Unions
Arbitrary and binary unions of sets.

axiom axiomOfUnion Union ∀ 1 ∃ 2 ∀ 3 ↔ ∈ 3 2 ∃ 4 ∧ ∈ 3 4 ∈ 4 1

theorem arbitraryUnionsExist Arbitrary Unions Exist
axiomOfUnion 1 2 3 4
eliminateForall 1 1 1
qed

theorem arbitraryUnionsAreUnique Arbitrary Unions Are Unique
arbitraryUnionsExist 2 3 4 1
setWithMembershipConditionIsUnique 1
qed

term union (term) (⋃{}) () (∀ 2 ↔ ∈ 2 union 1 ∃ 3 ∧ ∈ 2 3 ∈ 3 1)

theorem characteriseElementsOfArbitraryUnion Characterise Elements of Arbitrary Union
definition-union 2 1 3
eliminateForall 1 2 2
qed

theorem characteriseElementOfArbitraryUnion Characterise Element of Arbitrary Union
premise ∈ 2 union 1
characteriseElementsOfArbitraryUnion 2 1 3
modusPonensEquivalenceLeft 1 p1
qed

theorem elementOfElementIsElementOfUnion An Element of an Element of a Set Is an Element of the Union
premise ∈ 1 2
premise ∈ 2 3
introduceAnd p1 p2
introduceExists 1 ∧ ∈ 1 2 ∈ 2 3 2 2
characteriseElementsOfArbitraryUnion 1 3 2
modusPonensEquivalenceRight 3 2
qed

theorem unionOverEmptySetIsEmptySet Union over the Empty Set Is the Empty Set
definition-union 1 ∅ 2
definition-∅ 2
introduceAndInsideNegationRightInsideForall 2 ∈ 1' 2
forallNotIsNotExists 3
introduceForall 4 ¬ ∃ 2 ∧ ∈ 1 2 ∈ 2 ∅ 1' 1
modusTollensEquivalenceInsideForallRight 1 5
anEmptySetIsTheEmptySet 6
qed

theorem unionOverSingletonIsSet Union over a Singleton Is the Set
definition-union 1 singleton 2 3
assume ∃ 3 ∧ ∈ 1' 3 ∈ 3 singleton 2
  assume ∧ ∈ 1' 3' ∈ 3' singleton 2
    eliminateAndRight f.f.a
    elementOfSingletonIsSet f.f.1
    eliminateAndLeft f.f.a
    eliminateEquals f.f.2 f.f.3 ∈ 1' 2 2
  introduceImplication f.f.4
  eliminateExists f.a f.1 3'
introduceImplication f.2
assume ∈ 1' 2
  setIsElementOfSingleton 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 1' 3 ∈ 3 singleton 2 2 3
introduceImplication f.3
introduceEquivalence 2 3
introduceForall 4 ↔ ∃ 3 ∧ ∈ 1 3 ∈ 3 singleton 2 ∈ 1 2 1' 1
equivalenceIsTransitiveInsideForall 1 5
setsWithSameMembersEqual 6
qed

theorem unionPowerSetIsSet Union over a Power Set Is the Set
definition-union 1 powerSet 2 3
assume ∃ 3 ∧ ∈ 1' 3 ∈ 3 powerSet 2
  assume ∧ ∈ 1' 3' ∈ 3' powerSet 2
    eliminateAndRight f.f.a
    elementOfPowersetIsSubset f.f.1 3 2
    unapply-⊆ f.f.2 4
    eliminateForall f.f.3 1'
    eliminateAndLeft f.f.a
    eliminateImplication f.f.4 f.f.5
  introduceImplication f.f.6
  eliminateExists f.a f.1 3'
introduceImplication f.2
assume ∈ 1' 2
  anySetIsAnElementOfItsPowerSet 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 1' 3 ∈ 3 powerSet 2 2 3
introduceImplication f.3
introduceEquivalence 2 3
introduceForall 4 ↔ ∃ 3 ∧ ∈ 1 3 ∈ 3 powerSet 2 ∈ 1 2 1' 1
equivalenceIsTransitiveInsideForall 1 5
setsWithSameMembersEqual 6
qed

theorem unionOverPairMembershipCondition Union over Pair Membership Condition
definition-union 3 pair 1 2 4
assume ∃ 4 ∧ ∈ 3' 4 ∈ 4 pair 1 2
  assume ∧ ∈ 3' 4' ∈ 4' pair 1 2
    eliminateAndLeft f.f.a
    eliminateAndRight f.f.a
    characteriseElementOfPair f.f.2
    assume = 4' 1
      eliminateEquals f.f.f.a f.f.1 ∈ 3' 5 5
    introduceImplication f.f.f.1
    assume = 4' 2
      eliminateEquals f.f.f.a f.f.1 ∈ 3' 5 5
    introduceImplication f.f.f.1
    modusPonensOnOr f.f.3 f.f.4 f.f.5
  introduceImplication f.f.6
  eliminateExists f.a f.1 4'
introduceImplication f.2
assume ∈ 3' 1
  firstElementOfPair 1 2
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 3' 4 ∈ 4 pair 1 2 1 4
introduceImplication f.3
assume ∈ 3' 2
  secondElementOfPair 2 1
  introduceAnd f.a f.1
  introduceExists f.2 ∧ ∈ 3' 4 ∈ 4 pair 1 2 2 4
introduceImplication f.3
combineCases 3 4
introduceEquivalence 2 5
introduceForall 6 ↔ ∃ 4 ∧ ∈ 3 4 ∈ 4 pair 1 2 ∨ ∈ 3 1 ∈ 3 2 3' 3
equivalenceIsTransitiveInsideForall 1 7
qed

term ∪ (term term) () () (= ∪ 1 2 union pair 1 2)

theorem binaryUnionMembershipCondition Binary Union Membership Condition
definition-∪ 1 2
unionOverPairMembershipCondition 3 1 2
eliminateEqualsReverse 1 2 ∀ 3 ↔ ∈ 3 4 ∨ ∈ 3 1 ∈ 3 2 4
qed

theorem characteriseElementsOfBinaryUnion Characterise Elements of Binary Union
binaryUnionMembershipCondition 3 1 2
eliminateForall 1 3
qed

theorem unionIsSymmetric Union Is Symmetric
binaryUnionMembershipCondition 3 1 2
orIsSymmetric ∈ 3' 1 ∈ 3' 2
introduceForall 2 ↔ ∨ ∈ 3 1 ∈ 3 2 ∨ ∈ 3 2 ∈ 3 1 3' 3
equivalenceIsTransitiveInsideForall 1 3
binaryUnionMembershipCondition 3 2 1
equivalenceIsTransitiveInsideForallRight 4 5
setsWithSameMembersEqual 6
qed

chapter Intersections
Arbitrary and binary intersections.

theorem arbitraryIntersectionsExist Arbitrary Intersections Exist
premise ¬ = 1 ∅
nonEmptySetHasAnElement p1 3
assume ∈ 3' 1
  definition-comprehension ∀ 3 → ∈ 3 1 ∈ 4 3 2 4 3'
  assume ∀ 3 → ∈ 3 1 ∈ 2' 3
    eliminateForall f.f.a 3'
    eliminateImplication f.f.1 f.a
  introduceImplication f.f.2
  redundantAnd f.2
  introduceForall f.3 ↔ ∧ ∈ 2 3' ∀ 3 → ∈ 3 1 ∈ 2 3 ∀ 3 → ∈ 3 1 ∈ 2 3 2' 2
  equivalenceIsTransitiveInsideForall f.1 f.4
  introduceExists f.5 ∀ 2 ↔ ∈ 2 4 ∀ 3 → ∈ 3 1 ∈ 2 3 comprehension 4 3' ∀ 3 → ∈ 3 1 ∈ 4 3 4
introduceImplication f.6
eliminateExists 1 2 3'
qed

theorem arbitraryIntersectionsAreUnique Arbitrary Intersections Are Unique
premise ¬ = 1 ∅
arbitraryIntersectionsExist p1 4 2 3
setWithMembershipConditionIsUnique 1
qed

term intersection (term) (⋂{}) (¬ = 1 ∅) (∀ 2 ↔ ∈ 2 intersection 1 ∀ 3 → ∈ 3 1 ∈ 2 3)

theorem intersectionOverSingletonIsSet Intersection over Singleton Is Set
singletonIsNonEmpty 1
definition-intersection 1 2 3
assume ∀ 3 → ∈ 3 singleton 1 ∈ 2' 3
  eliminateForall f.a 1
  setIsElementOfSingleton 1
  eliminateImplication f.1 f.2
introduceImplication f.3
assume ∈ 2' 1
  quantifiedEqualityReplacement f.a ∈ 2' 3 1 3
  definition-singleton 3 1
  equivalenceThenImplicationInsideForall f.2 f.1
introduceImplication f.3
introduceEquivalence 3 4
introduceForall 5 ↔ ∀ 3 → ∈ 3 singleton 1 ∈ 2 3 ∈ 2 1 2' 2
equivalenceIsTransitiveInsideForall 2 6
setsWithSameMembersEqual 7
qed

theorem intersectionOverSetContainingEmptySetIsEmpty Intersection over a Set Containing the Empty Set Is Empty
premise ∈ ∅ 1
setWithAnElementIsNonEmpty p1
definition-intersection 1 2 3
assume ∀ 3 → ∈ 3 1 ∈ 2' 3
  eliminateForall f.a ∅
  eliminateImplication f.1 p1
  setIsNotInEmptySet 2'
introduceNegation f.2 f.3
introduceForall 3 ¬ ∀ 3 → ∈ 3 1 ∈ 2 3 2' 2
modusTollensEquivalenceInsideForallRight 2 4
anEmptySetIsTheEmptySet 5
qed
