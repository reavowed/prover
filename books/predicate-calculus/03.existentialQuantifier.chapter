chapter Existential Quantifier
Introduce the existential quantifier, ∃.

statement ∃ ($x with x φ)
  format ((∃x)φ)
  definition (¬ ∀ x ¬ with x φ)

theorem Existence
premise with a φ
assume ∀ x ¬ with x φ {
  prove ¬ with a φ
}
prove ¬ ∀ x ¬ with x φ
prove ∃ x with x φ
qed

comment If something exists, we can name that something with an arbitrary variable for free.

theorem Value for Existence
premise ∃ x with x φ elidable
premise ∀ x → with x φ ψ
prove ¬ ∀ x ¬ with x φ
assume ¬ ψ {
  take x {
    prove ¬ with x φ
  }
}
prove ψ
qed

comment Swapping an existence condition with an equivalent one.

theorem Existence Equivalence
premise ∃ x with x φ
premise ∀ x ↔ with x φ with x ψ
let a with a φ {
  prove with a ψ
  prove ∃ x with x ψ
}
qed

comment Swapping between universal and existential quantifiers.

theorem Not Exists Not Is For All
premise ¬ ∃ x ¬ with x φ
take x {
  assume ¬ with x φ {
    prove ∃ y ¬ with y φ
  }
  prove with x φ
}
qed

theorem Not Exists Is For All Not
premise ¬ ∃ x with x φ
assume ¬ ∀ x ¬ with x φ {
  prove ∃ x with x φ
}
prove ∀ x ¬ with x φ
qed

theorem For All Not Is Not Exists
expansion
premise ∀ x ¬ with x φ
assume ∃ x with x φ {
  prove ¬ ∀ x ¬ with x φ
}
prove ¬ ∃ x with x φ
qed
