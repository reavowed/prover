chapter Uniqueness Quantifier
Introduce the uniqueness quantifier, ∃!.

statement ∃! ($x with x φ)
  format ((∃!x)φ)
  definition (∧ ∃ x with x φ ∀ x ∀ y → ∧ with x φ with y φ = x y)

theorem Definition of ∃!
premise ∃ x with x φ
premise ∀ x ∀ y → ∧ with x φ with y φ = x y
prove ∧ ∃ x with x φ ∀ x ∀ y → ∧ with x φ with y φ = x y
prove ∃! x with x φ
qed

theorem Value for Unique Existence
premise ∃! x with x φ elidable
premise ∀ x → with x φ ψ
prove ∧ ∃ x with x φ ∀ x ∀ y → ∧ with x φ with y φ = x y
prove ψ
qed

theorem Every Set Is Unique
prove ∃ x = x a
take x { take y {
  assume ∧ = x a = y a {
    prove = x y
  }
}}
prove ∃! y = y a
qed

theorem Two Values Satisfying Unique Condition Are Equal
premise ∃! x with x φ
premise with a φ
premise with b φ
prove ∧ ∃ x with x φ ∀ x ∀ y → ∧ with x φ with y φ = x y
prove ∀ x ∀ y → ∧ with x φ with y φ = x y
prove ∀ y → ∧ with a φ with y φ = a y
prove → ∧ with a φ with b φ = a b
prove ∧ with a φ with b φ
prove = a b
qed

theorem Uniqueness by Exhibiting Unique Value
premise ∀ x ↔ with x φ = x a
prove = a a
prove with a φ
prove ∃ x with x φ
take x { take y {
  assume ∧ with x φ with y φ {
    prove = x a
    prove = y a
    prove = x y
  }
}}
prove ∃! x with x φ
qed

theorem Uniqueness by Exhibiting Unique Value
premise ∀ x → with x φ = x a
premise with a φ
take x {
  assume = x a {
    prove with x φ
  }
}
prove ∀ x ↔ with x φ = x a
prove ∃! x with x φ
qed

theorem Existence of Unique Value
premise ∃! x with x φ
let a with a φ {
  take x {
    assume with x φ {
      prove = x a
    }
    assume = x a  {
      prove with x φ
    }
    prove ↔ with x φ = x a
  }
  prove ∃ y ∀ x ↔ with x φ = x y
}
qed

theorem Unique Value Satisifies Condition
premise ∀ x ↔ with x φ = x a
prove = a a
prove with a φ
qed

theorem Uniqueness by Two Cases
premise ⊕ φ ψ
assume φ {
  prove ¬ ψ
  take x {
    prove ↔ ∨ ∧ φ = x a ∧ ψ = x b = x a
  }
  prove ∀ x ↔ ∨ ∧ φ = x a ∧ ψ = x b = x a
  prove ∃! x ∨ ∧ φ = x a ∧ ψ = x b
}
assume ψ {
  prove ¬ φ
  take x {
    prove ↔ ∨ ∧ φ = x a ∧ ψ = x b = x b
  }
  prove ∀ x ↔ ∨ ∧ φ = x a ∧ ψ = x b = x b
  prove ∃! x ∨ ∧ φ = x a ∧ ψ = x b
}
prove ∃! x ∨ ∧ φ = x a ∧ ψ = x b
qed

theorem Uniqueness by Two Cases
prove ⊕ φ ¬ φ
prove ∃! x ∨ ∧ φ = x a ∧ ¬ φ = x b
qed

# theorem Uniqueness Equivalence
# premise ∃! x φ
# premise ∀ x ↔ φ ψ
# prove ∧ ∃ x φ ∀ y ∀ z → ∧ sub y x φ sub z x φ = y z
# prove ∃ x ψ
# assume ∧ sub y x ψ sub z x ψ {
#   prove sub y x φ
#   prove sub z x φ
#   prove ∧ sub y x φ sub z x φ
#   prove ∀ z → ∧ sub y x φ sub z x φ = y z
#   prove → ∧ sub y x φ sub z x φ = y z
#   prove = y z
# }
# prove ∃! x ψ
# qed

# theorem Uniqueness Equivalence
# premise ∃! x φ
# premise proves φ ψ
# premise proves ψ φ
# prove ∀ x ↔ φ ψ
# prove ∃! x ψ
# qed
