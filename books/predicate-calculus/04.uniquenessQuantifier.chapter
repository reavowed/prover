Introduce the uniqueness quantifier, ∃!.

statement ∃! ($x with x φ)
  format ((∃!x)φ)
  definition (∧ ∃ x with ($0) φ ∀ x ∀ y → ∧ with ($$0) φ with ($0) φ = $$0 $0)

theorem Value for Unique Existence
premise ∃! x with ($0) φ elidable
premise ∀ x → with ($0) φ ψ
prove ∧ ∃ x with ($0) φ ∀ x ∀ y → ∧ with ($$0) φ with ($0) φ = $$0 $0
prove ψ
qed

theorem Every Set Is Unique
prove ∃ x = $0 a
take x {
  take y {
    assume ∧ = $$0 a = $0 a {
      prove = $$0 $0
    }
  }
}
prove ∃! y = $0 a
qed

theorem Two Values Satisfying Unique Condition Are Equal
premise ∃! x with ($0) φ
premise with (a) φ
premise with (b) φ
prove ∧ ∃ x with ($0) φ ∀ x ∀ y → ∧ with ($$0) φ with ($0) φ = $$0 $0
prove ∀ y → ∧ with (a) φ with ($0) φ = a $0
prove → ∧ with (a) φ with (b) φ = a b
prove = a b
qed

theorem Uniqueness by Exhibiting Unique Value
premise ∃ x with ($0) φ
premise ∀ x → with ($0) φ = $0 a
take x {
  take y {
    assume ∧ with ($$0) φ with ($0) φ {
      prove = $$0 a
      prove = $0 a
      prove = $$0 $0
    }
  }
}
prove ∃! x with ($0) φ
qed

theorem Uniqueness by Exhibiting Unique Value
premise ∀ x ↔ with ($0) φ = $0 a
prove = a a
prove with (a) φ
prove ∃ x with ($0) φ
prove ∀ x → with ($0) φ = $0 a
prove ∃! x with ($0) φ
qed

theorem Uniqueness by Exhibiting Unique Value
premise ∀ x → with ($0) φ = $0 a
premise with (a) φ
take x {
  assume = $0 a {
    prove with ($0) φ
  }
}
prove ∀ x ↔ with ($0) φ = $0 a
prove ∃! x with ($0) φ
qed

theorem Existence of Unique Value
premise ∃! x with ($0) φ
let a with ($0) φ {
  take x {
    assume with ($0) φ {
      prove = $0 $$0
    }
    assume = $0 $$0 {
      prove with ($0) φ
    }
    prove ↔ with ($0) φ = $0 $$0
  }
  prove ∃ y ∀ x ↔ with ($0) φ = $0 $$0
}
qed

theorem Unique Value Satisifies Condition
premise ∀ x ↔ with ($0) φ = $0 a
prove = a a
prove with (a) φ
qed

theorem Uniqueness by Two Cases
premise ⊕ φ ψ
assume φ {
  prove ¬ ψ
  take x {
    prove ↔ ∨ ∧ φ = $0 a ∧ ψ = $0 b = $0 a
  }
  prove ∃! x ∨ ∧ φ = $0 a ∧ ψ = $0 b
}
assume ψ {
  prove ¬ φ
  take x {
    prove ↔ ∨ ∧ φ = $0 a ∧ ψ = $0 b = $0 b
  }
  prove ∃! x ∨ ∧ φ = $0 a ∧ ψ = $0 b
}
prove ∃! x ∨ ∧ φ = $0 a ∧ ψ = $0 b
qed

theorem Uniqueness by Two Cases
prove ⊕ φ ¬ φ
prove ∃! x ∨ ∧ φ = $0 a ∧ ¬ φ = $0 b
qed
