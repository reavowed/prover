chapter Equality
Introduce equality and its associated rules of inference.

statement = (a b)

shorthand ¬ = a b as (a ≠ b in parens)

axiom Equality Is Reflexive
conclusion = a a

axiom Substitution of Equals
premise = a b elidable
premise with a φ
conclusion with b φ

comment We first have to prove that equality is symmetric before we can prove the reversed version of the second axiom.

theorem Reverse Equality
premise = a b
prove = a a
prove = b a
qed

theorem Substitution of Equals
premise = a b elidable
premise with b φ
prove = b a
prove with a φ
qed

comment Now we can also prove that the substitution axiom works when substituting into equal terms.

theorem Substitution of Equals
premise = a b elidable
prove = with a F with a F
prove = with a F with b F
qed

theorem Substitution of Equals
premise = a b elidable
prove = b a
prove = with b F with a F
qed

comment Basic facts about equality.

theorem Equality Is Symmetric
assume = a b {
  prove = b a
}
assume = b a {
  prove = a b
}
prove ↔ = a b = b a
qed

theorem Equality Is Transitive
premise = a b
premise = b c
prove = a c
qed

theorem Equivalence of Substituted Equals
premise = a b
assume with a φ {
  prove with b φ
}
assume with b φ {
  prove with a φ
}
prove ↔ with a φ with b φ
qed

theorem Distinguishable Terms Are Not Equal
premise with a φ
premise ¬ with b φ
assume = a b {
  prove with b φ
}
prove ¬ = a b
qed

theorem Sets Are Equal If Always Equal to the Same Thing
premise ∀ x ↔ = x a = x b
prove = a a
prove = a b
qed

theorem Every Set Exists
prove = a a
prove ∃ x = x a
qed

theorem Renamed Set
premise ∀ x → = x a φ
prove ∃ x = x a
prove φ
qed

theorem Exclusive Or for Equality
premise ∨ = a b = a c
premise ¬ = b c
assume ∧ = a b = a c {
  prove = b c
}
prove ¬ ∧ = a b = a c
prove ⊕ = a b = a c
qed
