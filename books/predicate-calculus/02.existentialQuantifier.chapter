Introduce the existential quantifier, ∃.

statement ∃ ($x with x φ)
  format ((∃x)φ)
  definition (¬ ∀ x ¬ with ($0) φ)

theorem Existence
premise with (a) φ
assume ∀ x ¬ with ($0) φ {
  prove ¬ with (a) φ
}
prove ¬ ∀ x ¬ with ($0) φ
prove ∃ x with ($0) φ
qed

comment If something exists, we can name that something with an arbitrary variable for free.

theorem Value for Existence
premise ∃ x with ($0) φ elidable
premise ∀ x → with ($0) φ ψ
prove ¬ ∀ x ¬ with ($0) φ
assume ¬ ψ {
  take x {
    prove ¬ with ($0) φ
  }
}
prove ψ
qed

comment Swapping an existence condition with an equivalent one.

theorem Existence Equivalence
premise ∃ x with ($0) φ
premise ∀ x ↔ with ($0) φ with ($0) ψ
let a with ($0) φ {
  prove with ($0) ψ
  prove ∃ x with ($$0) ψ
}
qed

comment Swapping between universal and existential quantifiers.

theorem Not Exists Not Is For All
premise ¬ ∃ x ¬ with ($0) φ
take x {
  assume ¬ with ($0) φ {
    prove ∃ y ¬ with ($$0) φ
  }
  prove with ($0) φ
}
qed

theorem Not Exists Is For All Not
premise ¬ ∃ x with ($0) φ
assume ¬ ∀ x ¬ with ($0) φ {
  prove ∃ x with ($0) φ
}
prove ∀ x ¬ with ($0) φ
qed

theorem For All Not Is Not Exists
premise ∀ x ¬ with ($0) φ
assume ∃ x with ($0) φ {
  prove ¬ ∀ x ¬ with ($0) φ
}
prove ¬ ∃ x with ($0) φ
qed
