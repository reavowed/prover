chapter Definition of a Function
Definition of a function.

statement function (f X Y)
  name (a function)
  format (f: X → Y in parens)
  definition (∧ ⊆ f × X Y ∀ x → ∈ x X ∃! y ∈ orderedPair x y f)

theorem Definition of a Function
premise ⊆ f × X Y
premise ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove ∧ ⊆ f × X Y ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove function f X Y
qed

theorem Definition of a Function
premise ⊆ f × X Y
premise proves ∈ x X ∃! y ∈ orderedPair x y f
prove ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove ∧ ⊆ f × X Y ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove function f X Y
qed

theorem Function Is Subset of Product
premise function f X Y
prove ∧ ⊆ f × X Y ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove ⊆ f × X Y
qed

theorem Function Application Is Unique
premise function f X Y
premise ∈ x X
prove ∧ ⊆ f × X Y ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove ∀ x → ∈ x X ∃! y ∈ orderedPair x y f
prove ∃! y ∈ orderedPair x y f
qed

theorem First Coordinate of Element of Function Is Element of Domain
premise function f X Y
premise ∈ orderedPair x y f
prove ⊆ f × X Y
prove ∈ orderedPair x y × X Y
prove ∈ x X
qed

theorem Second Coordinate of Element of Function Is Element of Range
premise function f X Y
premise ∈ orderedPair x y f
prove ⊆ f × X Y
prove ∈ orderedPair x y × X Y
prove ∈ y Y
qed

theorem Extend Function Range to Superset
premise function f A X
premise ⊆ X Y
prove ∧ ⊆ f × A X ∀ x → ∈ x A ∃! y ∈ orderedPair x y f
prove ⊆ × A X × A Y
prove ⊆ f × A Y
prove function f A Y
qed

term apply (f a)
  name (Function Application)
  format (f(a))
  premises (function f A B, ∈ a A)
  (∈ orderedPair a _ f)

theorem Function Application Is Element of Range
premise function f X Y
premise ∈ x X
prove ⊆ f × X Y
prove ∈ orderedPair x apply f x f
prove ∈ orderedPair x apply f x × X Y
prove ∈ apply f x Y
qed

theorem Uniqueness of Function Application
premise function f X Y
premise ∈ orderedPair x y f
prove ∈ x X
prove ∃! z ∈ orderedPair x z f
prove ∈ orderedPair x apply f x f
prove = y apply f x
qed

theorem Uniqueness of Function Application
premise function f X Y
premise ∈ orderedPair x y f
prove = y apply f x
prove = apply f x y
qed
