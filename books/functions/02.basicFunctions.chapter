Define some basic functions.

theorem The Empty Set Is a Function with Empty Domain
prove ⊆ ∅ × ∅ A
prove ∀ x ¬ ∈ $0 ∅
prove ∀ x → ∈ $0 ∅ ∃! y ∈ orderedPair $.0 $$0 ∅
prove function ∅ ∅ A
qed

theorem Function with a Singleton Domain
prove ∈ a singleton a
prove ∈ b singleton b
prove ∈ orderedPair a b × singleton a singleton b
prove ⊆ singleton orderedPair a b × singleton a singleton b
take x {
  assume ∈ $0 singleton a {
    prove = $0 a
    take y {
      assume ∈ orderedPair $.0 $$0 singleton orderedPair a b {
        prove = orderedPair $.0 $$0 orderedPair a b
        prove = $$0 b
      }
      assume = $$0 b {
        prove ∈ orderedPair a b singleton orderedPair a b
        prove ∈ orderedPair $.0 b singleton orderedPair a b
        prove ∈ orderedPair $.0 $$0 singleton orderedPair a b
      }
    }
    prove ∃! y ∈ orderedPair $.0 $$0 singleton orderedPair a b
  }
}
prove function singleton orderedPair a b singleton a singleton b
qed

theorem Combine Functions
premise function f A B
premise function g C D
premise ∀ x → ∈ $0 ∩ A C = apply f $0 apply g $0
prove ⊆ f × A B
prove ⊆ × A B × ∪ A C B
  via ⊆ A ∪ A C
prove ⊆ f × ∪ A C B
prove ⊆ × ∪ A C B × ∪ A C ∪ B D
  via ⊆ B ∪ B D
prove ⊆ f × ∪ A C ∪ B D
prove ⊆ g × C D
prove ⊆ × C D × ∪ A C D
  via ⊆ C ∪ A C
prove ⊆ g × ∪ A C D
prove ⊆ × ∪ A C D × ∪ A C ∪ B D
  via ⊆ D ∪ B D
prove ⊆ g × ∪ A C ∪ B D
prove ⊆ ∪ f g × ∪ A C ∪ B D
take x {
  assume ∈ $0 ∪ A C {
    prove ∨ ∈ $0 A ∈ $0 C
    assume ∈ $0 A {
      take y {
        assume ∈ orderedPair $.0 $$0 ∪ f g {
          prove ∨ ∈ orderedPair $.0 $$0 f ∈ orderedPair $.0 $$0 g
          assume ∈ orderedPair $.0 $$0 f {
            prove = $$0 apply f $.0
          }
          assume ∈ orderedPair $.0 $$0 g {
            prove ∈ $.0 C
            prove ∈ $.0 ∩ A C
            prove = apply f $.0 apply g $.0
            prove = $$0 apply g $.0
            prove = $$0 apply f $.0
          }
          prove = $$0 apply f $.0
        }
      }
      prove ∈ orderedPair $0 apply f $0 f
      prove ∈ orderedPair $0 apply f $0 ∪ f g
      prove ∃! y ∈ orderedPair $.0 $$0 ∪ f g
    }
    assume ∈ $0 C {
      take y {
        assume ∈ orderedPair $.0 $$0 ∪ f g {
          prove ∨ ∈ orderedPair $.0 $$0 f ∈ orderedPair $.0 $$0 g
          assume ∈ orderedPair $.0 $$0 f {
            prove ∈ $.0 A
            prove ∈ $.0 ∩ A C
            prove = apply f $.0 apply g $.0
            prove = $$0 apply f $.0
            prove = $$0 apply g $.0
          }
          assume ∈ orderedPair $.0 $$0 g {
            prove = $$0 apply g $.0
          }
          prove = $$0 apply g $.0
        }
      }
      prove ∈ orderedPair $0 apply g $0 g
      prove ∈ orderedPair $0 apply g $0 ∪ f g
      prove ∃! y ∈ orderedPair $.0 $$0 ∪ f g
    }
    prove ∃! y ∈ orderedPair $.0 $$0 ∪ f g
  }
}
prove function ∪ f g ∪ A C ∪ B D
qed

theorem Combine Disjoint Functions
premise function f A B
premise function g C D
premise = ∩ A C ∅
prove ∀ x ¬ ∈ $0 ∩ A C
  via ∀ x ¬ ∈ $0 ∅
prove ∀ x → ∈ $0 ∩ A C = apply f $0 apply g $0
prove function ∪ f g ∪ A C ∪ B D
qed

theorem Extending a Function by One Value
premise function f A B
premise ¬ ∈ a A
premise ∈ b B
take x {
  assume ∈ $0 A {
    assume ∈ $0 singleton a {
      prove = $0 a
      prove ∈ a A
    }
    prove ¬ ∈ $0 singleton a
  }
}
prove = ∩ A singleton a ∅
prove function singleton orderedPair a b singleton a singleton b
prove function ∪ f singleton orderedPair a b ∪ A singleton a ∪ B singleton b
prove function ∪ f singleton orderedPair a b ∪ A singleton a B
  via = ∪ B singleton b B
qed
