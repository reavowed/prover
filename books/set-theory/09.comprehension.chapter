Introduce set comprehension, {x ∈ A | φ(x)}.

theorem Comprehensions Exist for Empty Sets
premise ¬ ∃ x ∧ ∈ $0 A with ($0) φ
prove ∀ x ¬ ∧ ∈ $0 A with ($0) φ
prove ∀ x ↔ ∈ $0 ∅ ∧ ∈ $0 A with ($0) φ
prove ∃ y ∀ x ↔ ∈ $0 $$0 ∧ ∈ $0 A with ($0) φ
prove ∃! y ∀ x ↔ ∈ $0 $$0 ∧ ∈ $0 A with ($0) φ
qed

theorem Comprehensions Exist for Non-Empty Sets Lemma
premise ∧ ∈ a A with (a) φ
take y {
  assume ∃ x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a {
    let x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a {
      assume with ($0) φ {
        prove = $$0 $0
          via ↔ ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a = $$0 $0
        prove ∧ ∈ $0 A with ($0) φ
        prove ∧ ∈ $$0 A with ($$0) φ
      }
      assume ¬ with ($0) φ {
        prove = $$0 a
          via ↔ ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a = $$0 a
        prove ∧ ∈ $$0 A with ($$0) φ
      }
      prove ∧ ∈ $$0 A with ($$0) φ
    }
  }
  assume ∧ ∈ $0 A with ($0) φ {
    prove = $0 $0
    prove ∧ ∈ $0 A ∨ ∧ with ($0) φ = $0 $0 ∧ ¬ with ($0) φ = $0 a
    prove ∃ x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a
  }
  prove ↔ ∃ x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 a ∧ ∈ $0 A with ($0) φ
}
qed

theorem Comprehensions Exist for Non-Empty Sets
premise ∃ y ∧ ∈ $0 A with ($0) φ
let a ∧ ∈ $0 A with ($0) φ {
  take x {
    prove ∃! y ∨ ∧ with ($$0) φ = $0 $$0 ∧ ¬ with ($$0) φ = $0 $$$0
    prove → ∈ $0 A ∃! y ∨ ∧ with ($$0) φ = $0 $$0 ∧ ¬ with ($$0) φ = $0 $$$0
  }
  prove ∃ Y ∀ y ↔ ∈ $0 $$0 ∃ x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 $$$$0
  prove ∀ y ↔ ∃ x ∧ ∈ $0 A ∨ ∧ with ($0) φ = $$0 $0 ∧ ¬ with ($0) φ = $$0 $$$0 ∧ ∈ $0 A with ($0) φ
  prove ∃ Y ∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 A with ($0) φ
}
prove ∃! Y ∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 A with ($0) φ
qed

theorem Comprehensions Exist
assume ¬ ∃ y ∧ ∈ $0 X with ($0) φ {
  prove ∃! Y ∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 X with ($0) φ
}
assume ∃ y ∧ ∈ $0 X with ($0) φ {
  prove ∃! Y ∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 X with ($0) φ
}
prove ∃! Y ∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 X with ($0) φ
qed

term comprehension ($x A with x φ)
  name (Comprehension)
  format ({x ∈ A | φ})
  (∀ y ↔ ∈ $0 $$0 ∧ ∈ $0 A with ($0) φ)

theorem Membership Condition for a Comprehension
premise = S comprehension x A with ($0) φ
prove ∀ y ↔ ∈ $0 comprehension x A with ($0) φ ∧ ∈ $0 A with ($0) φ
prove ∀ y ↔ ∈ $0 S ∧ ∈ $0 A with ($0) φ
qed

theorem An Element of a Comprehension Is an Element of Its Domain
premise = S comprehension x A with ($0) φ
premise ∈ a S
prove ∀ y ↔ ∈ $0 S ∧ ∈ $0 A with ($0) φ
prove ↔ ∈ a S ∧ ∈ a A with (a) φ
prove ∧ ∈ a A with (a) φ
prove ∈ a A
qed

theorem An Element of a Comprehension Satisfies Its Condition
premise = S comprehension x A with ($0) φ
premise ∈ a S
prove ∀ y ↔ ∈ $0 S ∧ ∈ $0 A with ($0) φ
prove ↔ ∈ a S ∧ ∈ a A with (a) φ
prove ∧ ∈ a A with (a) φ
prove with (a) φ
qed

theorem A Comprehension Is a Subset of Its Domain
premise = S comprehension x A with ($0) φ
take y {
  assume ∈ $0 S {
    prove ∈ $0 A
  }
}
prove ⊆ S A
qed

theorem Element of a Comprehension
premise = S comprehension x A with ($0) φ
premise ∈ a A
premise with (a) φ
prove ∀ y ↔ ∈ $0 S ∧ ∈ $0 A with ($0) φ
prove ∈ a S
qed

theorem Universal Property from Comprehension
premise = S comprehension x A with ($0) φ
premise = S A
take y {
  assume ∈ $0 A {
    prove ∈ $0 S
    prove with ($0) φ
  }
}
qed
