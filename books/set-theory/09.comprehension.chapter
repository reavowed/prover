chapter Comprehension
Introduce set comprehension, {x ∈ X | φ}.

theorem Comprehensions Exist for Empty Sets
premise ¬ ∃ y ∧ ∈ y X sub y x φ
prove ∀ y ¬ ∧ ∈ y X sub y x φ
prove ∀ y ↔ ∈ y ∅ ∧ ∈ y X sub y x φ
prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist for Non-Empty Sets Lemma
premise ∧ ∈ a X sub a x φ
prove ∈ a X
prove sub a x φ
assume ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a {
  let x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a {
    assume φ {
      prove ↔ ∨ ∧ φ = y x ∧ ¬ φ = y a = y x
      prove = y x
      prove ∈ y X
      prove sub y x φ
      prove ∧ ∈ y X sub y x φ
    }
    assume ¬ φ {
      prove ↔ ∨ ∧ φ = y x ∧ ¬ φ = y a = y a
      prove = y a
      prove ∈ y X
      prove sub y x φ
      prove ∧ ∈ y X sub y x φ
    }
    prove ∧ ∈ y X sub y x φ
  }
}
assume ∧ ∈ y X sub y x φ {
  prove = y y
  prove ∧ ∈ y X ∨ ∧ sub y x φ = y y ∧ ¬ sub y x φ = y a
  prove ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a
}
prove ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
prove ∀ y ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist for Non-Empty Sets
premise ∃ y ∧ ∈ y X sub y x φ
let a ∧ ∈ a X sub a x φ {
  prove ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove → ∈ x X ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∀ x → ∈ x X ∃! y ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∃ Y ∀ y ↔ ∈ y Y ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a
  prove ∀ y ↔ ∃ x ∧ ∈ x X ∨ ∧ φ = y x ∧ ¬ φ = y a ∧ ∈ y X sub y x φ
  prove ∃ Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

theorem Comprehensions Exist
assume ¬ ∃ y ∧ ∈ y X sub y x φ {
  prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
assume ∃ y ∧ ∈ y X sub y x φ {
  prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
}
prove ∃! Y ∀ y ↔ ∈ y Y ∧ ∈ y X sub y x φ
qed

term comprehension (x X φ)
  format ({x ∈ X | φ})
  (∀ y ↔ ∈ y _ ∧ ∈ y X sub y x φ)
  distinct-variables (y φ)

theorem Membership Condition for a Comprehension
premise = S comprehension x X φ
prove ∀ y ↔ ∈ y comprehension x X φ ∧ ∈ y X sub y x φ
prove ∀ y ↔ ∈ y S ∧ ∈ y X sub y x φ
qed

theorem An Element of a Comprehension Is an Element of Its Domain
premise = S comprehension x X φ
premise ∈ a S
prove ∀ y ↔ ∈ y S ∧ ∈ y X sub y x φ
prove ↔ ∈ a S ∧ ∈ a X sub a x φ
prove ∧ ∈ a X sub a x φ
prove ∈ a X
qed

theorem An Element of a Comprehension Satisfies Its Condition
premise = S comprehension x X φ
premise ∈ a S
prove ∀ y ↔ ∈ y S ∧ ∈ y X sub y x φ
prove ↔ ∈ a S ∧ ∈ a X sub a x φ
prove ∧ ∈ a X sub a x φ
prove sub a x φ
qed

theorem A Comprehension Is a Subset of Its Domain
premise = S comprehension x X φ
assume ∈ y S {
  prove ∈ y X
}
prove ∀ y → ∈ y S ∈ y X
prove ⊆ S X
qed

theorem Element of a Comprehension
premise = S comprehension x X φ
premise ∈ a X
premise sub a x φ
prove ∀ y ↔ ∈ y S ∧ ∈ y X sub y x φ
prove ∧ ∈ a X sub a x φ
prove ∈ a S
qed

theorem Universal Property from Comprehension
premise = S comprehension x X φ
premise = S X
assume ∈ y X {
  prove ∈ y S
  prove sub y x φ
}
prove ∀ x → ∈ x X φ
qed
