chapter Cartesian Product
Introduce the Cartesian product of two sets, a×b.

theorem Ordered Pair Is Element of Double Power Set of Union
premise ∈ a A
premise ∈ b B
prove ∈ a ∪ A B
prove ∈ b ∪ A B
prove ∈ singleton a powerSet ∪ A B
prove ∈ pair a b powerSet ∪ A B
prove ∈ pair singleton a pair a b powerSet powerSet ∪ A B
prove = orderedPair a b pair singleton a pair a b
prove ∈ orderedPair a b powerSet powerSet ∪ A B
qed

term × (A B)
  name (Cartesian Product)
  (= _ comprehension x powerSet powerSet ∪ A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b)

theorem Membership Condition for Cartesian Product
prove = × A B comprehension y powerSet powerSet ∪ A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = y orderedPair a b
prove ∀ x ↔ ∈ x × A B ∧ ∈ x powerSet powerSet ∪ A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b
take x {
  assume ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b {
    let c ∧ ∈ c A ∃ b ∧ ∈ b B = x orderedPair c b {
      let d ∧ ∈ d B = x orderedPair c d {
        prove ∈ orderedPair c d powerSet powerSet ∪ A B
        prove ∈ x powerSet powerSet ∪ A B
      }
    }
  }
  prove ↔ ∧ ∈ x powerSet powerSet ∪ A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b
}
prove ∀ x ↔ ∈ x × A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b
qed

theorem Element of Cartesian Product
premise ∈ X × A B
prove ∀ x ↔ ∈ x × A B ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b
prove ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = X orderedPair a b
qed

theorem Membership Condition for Cartesian Product
take a { take b {
  assume ∈ orderedPair a b × A B {
    prove ∃ c ∧ ∈ c A ∃ d ∧ ∈ d B = orderedPair a b orderedPair c d
    let c ∧ ∈ c A ∃ d ∧ ∈ d B = orderedPair a b orderedPair c d {
      let d ∧ ∈ d B = orderedPair a b orderedPair c d {
        prove = a c
        prove = b d
        prove ∈ a A
        prove ∈ b B
        prove ∧ ∈ a A ∈ b B
      }
    }
  }
  assume ∧ ∈ a A ∈ b B {
    prove ∈ a A
    prove ∈ b B
    prove = orderedPair a b orderedPair a b
    prove ∧ ∈ b B = orderedPair a b orderedPair a b
    prove ∃ d ∧ ∈ d B = orderedPair a b orderedPair a d
    prove ∧ ∈ a A ∃ d ∧ ∈ d B = orderedPair a b orderedPair a d
    prove ∃ c ∧ ∈ c A ∃ d ∧ ∈ d B = orderedPair a b orderedPair c d
    prove ∀ x ↔ ∈ x × A B ∃ c ∧ ∈ c A ∃ d ∧ ∈ d B = x orderedPair c d
    prove ∈ orderedPair a b × A B
  }
  prove ↔ ∈ orderedPair a b × A B ∧ ∈ a A ∈ b B
}}
prove ∀ a ∀ b ↔ ∈ orderedPair a b × A B ∧ ∈ a A ∈ b B
qed

theorem Ordered Pair Is Element of Cartesian Product
premise ∈ a A
premise ∈ b B
prove ∧ ∈ a A ∈ b B
prove ∀ x ∀ y ↔ ∈ orderedPair x y × A B ∧ ∈ x A ∈ y B
prove ∀ y ↔ ∈ orderedPair a y × A B ∧ ∈ a A ∈ y B
prove ↔ ∈ orderedPair a b × A B ∧ ∈ a A ∈ b B
prove ∈ orderedPair a b × A B
qed

theorem Coordinates of Element of Cartesian Product
premise ∈ orderedPair a b × A B
prove ∀ x ∀ y ↔ ∈ orderedPair x y × A B ∧ ∈ x A ∈ y B
prove ∀ y ↔ ∈ orderedPair a y × A B ∧ ∈ a A ∈ y B
prove ↔ ∈ orderedPair a b × A B ∧ ∈ a A ∈ b B
prove ∧ ∈ a A ∈ b B
qed

theorem First Coordinate of Element of Cartesian Product
premise ∈ orderedPair a b × A B
prove ∧ ∈ a A ∈ b B
prove ∈ a A
qed

theorem Second Coordinate of Element of Cartesian Product
premise ∈ orderedPair a b × A B
prove ∧ ∈ a A ∈ b B
prove ∈ b B
qed

theorem Product of Subsets Is Subset of Product
premise ⊆ A C
premise ⊆ B D
take x {
  assume ∈ x × A B {
    prove ∃ a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b
    let a ∧ ∈ a A ∃ b ∧ ∈ b B = x orderedPair a b {
      let b ∧ ∈ b B = x orderedPair a b {
        prove ∈ a C
        prove ∈ b D
        prove ∈ orderedPair a b × C D
        prove ∈ x × C D
      }
    }
  }
}
prove ⊆ × A B × C D
qed

theorem Product Preserves Subset
premise ⊆ A B
prove ⊆ C C
prove ⊆ × A C × B C
qed

theorem Product Preserves Subset
premise ⊆ A B
prove ⊆ C C
prove ⊆ × C A × C B
qed
