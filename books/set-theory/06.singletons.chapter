Establish the existence of the singleton set {a}.

theorem There Exists a Set with an Element
prove ∈ ∅ powerSet ∅
prove ∃ y ∈ y powerSet ∅
prove ∃ x ∃ y ∈ y x
qed

theorem Singleton Existence
prove ∃ x ∃ y ∈ y x
let b ∃ y ∈ y b {
  prove ∃! x = x a
  take y {
    prove → ∈ y b ∃! x = x a
  }
  prove ∃ z ∀ x ↔ ∈ x z ∃ y ∧ ∈ y b = x a
  take x {
    prove ↔ ∃ y ∧ ∈ y b = x a = x a
  }
  prove ∃ z ∀ x ↔ ∈ x z = x a
  prove ∃! z ∀ x ↔ ∈ x z = x a
}
qed

term singleton (a)
  name (Singleton)
  format ({a})
  (∀ x ↔ ∈ x _ = x a)

theorem A Set Is an Element of Its Singleton
prove ∀ x ↔ ∈ x singleton a = x a
prove ∈ a singleton a
qed

theorem Element of Singleton
premise ∈ b singleton a
prove ∀ x ↔ ∈ x singleton a = x a
prove = b a
qed

theorem Sets Are Equal If Singletons Are Equal
premise = singleton a singleton b
prove ∀ x ↔ ∈ x singleton a = x a
prove ∀ x ↔ ∈ x singleton b = x a
prove ∀ x ↔ ∈ x singleton b = x b
prove ∀ x ↔ = x a = x b
prove = a b
qed

theorem Singleton of Element Is Subset
premise ∈ a S
prove ∀ x ↔ ∈ x singleton a = x a
take x {
  assume = x a {
    prove ∈ x S
  }
}
prove ∀ x → ∈ x singleton a ∈ x S
prove ⊆ singleton a S
qed

theorem Singleton Is in Power Set
premise ∈ a S
prove ⊆ singleton a S
prove ∈ singleton a powerSet S
qed

theorem A Singleton Is Not the Empty Set
prove ∈ a singleton a
prove ¬ = singleton a ∅
qed

theorem Subsets of a Singleton
premise ⊆ S singleton a
prove ∀ x → ∈ x S ∈ x singleton a
prove ∀ x ↔ ∈ x singleton a = x a
prove ∀ x → ∈ x S = x a
assume ∃ y ∈ y S {
  let b ∈ b S {
    prove = b a
    prove ∈ a S
  }
  prove ⊆ singleton a S
  prove = S singleton a
}
assume ¬ ∃ y ∈ y S {
  prove = S ∅
}
prove ∨ = S singleton a = S ∅
prove ¬ = singleton a ∅
prove ⊕ = S singleton a = S ∅
qed

theorem Elements of Powerset of Singleton
premise ∈ S powerSet singleton a
prove ⊆ S singleton a
prove ⊕ = S singleton a = S ∅
qed

