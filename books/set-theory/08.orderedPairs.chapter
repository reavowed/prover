Establish the existence of the ordered pair (a, b).

term orderedPair (a b)
  name (Ordered Pair)
  format ((a, b))
  (= $0 pair singleton a pair a b)

theorem Elements of Ordered Pair
premise ∈ c orderedPair a b
prove = orderedPair a b pair singleton a pair a b
prove ∈ c pair singleton a pair a b
prove ∨ = c singleton a = c pair a b
qed

theorem Singleton of First Coordinate Is Element of Ordered Pair
prove = orderedPair a b pair singleton a pair a b
prove ∈ singleton a pair singleton a pair a b
prove ∈ singleton a orderedPair a b
qed

theorem Pair Is Element of Ordered Pair
prove = orderedPair a b pair singleton a pair a b
prove ∈ pair a b pair singleton a pair a b
prove ∈ pair a b orderedPair a b
qed

theorem Ordered Pair with Same Coordinates Is Double Singleton
prove = orderedPair a a pair singleton a pair a a
prove = orderedPair a a pair singleton a singleton a
  via = pair a a singleton a
prove = orderedPair a a singleton singleton a
  via = pair singleton a singleton a singleton singleton a
qed

theorem Ordered Pair has Same Coordinates If It Is a Double Singleton
premise = orderedPair a b singleton singleton c
prove ∈ pair a b orderedPair a b
prove ∈ pair a b singleton singleton c
prove = pair a b singleton c
prove = a b
qed

theorem First Coordinates of Equal Ordered Pairs Are Equal
premise = orderedPair a b orderedPair c d
prove ∈ singleton a orderedPair a b
prove ∈ singleton a orderedPair c d
prove ∨ = singleton a singleton c = singleton a pair c d
assume = singleton a singleton c {
  prove = a c
}
assume = singleton a pair c d {
  prove = pair c d singleton a
  prove = c a
  prove = a c
}
prove = a c
qed

theorem Second Coordinates of Equal Ordered Pairs Are Equal - Lemma 1
premise = orderedPair a b orderedPair c d
premise = b a
prove = a c
prove = orderedPair a b orderedPair a d
prove = orderedPair a a singleton singleton a
prove = orderedPair a b singleton singleton a
prove = orderedPair a d singleton singleton a
prove = a d
prove = b d
qed

theorem Second Coordinates of Equal Ordered Pairs Are Equal
premise = orderedPair a b orderedPair c d
prove = a c
prove = orderedPair a b orderedPair a d
prove ∈ pair a b orderedPair a b
prove ∈ pair a b orderedPair a d
prove ∨ = pair a b singleton a = pair a b pair a d
assume = pair a b singleton a {
  prove = b a
  prove = b d
}
assume = pair a b pair a d {
  prove ∈ b pair a b
  prove ∈ b pair a d
  prove ∨ = b a = b d
  assume = b a {
    prove = b d
  }
  prove = b d
}
prove = b d
qed
