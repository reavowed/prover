chapter Regularity
Introduce the Axiom of Regularity.

axiom Axiom of Regularity
conclusion ∀ x → ¬ = x ∅ ∃ y ∧ ∈ y x = ∩ y x ∅

theorem Regularity
premise ¬ = a ∅
prove ∀ y → ¬ = y ∅ ∃ x ∧ ∈ x y = ∩ x y ∅
prove ∃ x ∧ ∈ x a = ∩ x a ∅
qed

theorem No Set Contains Itself
prove ¬ = singleton a ∅
prove ∃ y ∧ ∈ y singleton a = ∩ y singleton a ∅
let y ∧ ∈ y singleton a = ∩ y singleton a ∅ {
  prove = y a
  prove = ∩ a singleton a ∅
}
prove ∈ a singleton a
prove ¬ ∈ a a
qed

theorem No Set Contains Itself
premise ∈ a b
prove ¬ ∈ b b
prove ¬ = a b
qed

theorem No Two Sets Contain Each Other
prove ¬ = pair a b ∅
prove ∃ x ∧ ∈ x pair a b = ∩ x pair a b ∅
let c ∧ ∈ c pair a b = ∩ c pair a b ∅ {
  prove ∨ = c a = c b
  assume = c a {
    prove = ∩ a pair a b ∅
    prove ∈ b pair a b
    prove ¬ ∈ b a
  }
  assume = c b {
    prove = ∩ b pair a b ∅
    prove ∈ a pair a b
    prove ¬ ∈ a b
  }
  prove ∨ ¬ ∈ b a ¬ ∈ a b
}
qed

theorem No Two Sets Contain Each Other
prove ∨ ¬ ∈ b a ¬ ∈ a b
prove ¬ ∧ ∈ b a ∈ a b
qed
