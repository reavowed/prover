chapter Unions
Arbitrary and binary unions.

axiom Axiom of Union
conclusion ∃ x ∀ y ↔ ∈ y x ∃ z ∧ ∈ z a ∈ y z

theorem Arbitrary Unions Exist
prove ∃ x ∀ y ↔ ∈ y x ∃ z ∧ ∈ z a ∈ y z
prove ∃! x ∀ y ↔ ∈ y x ∃ z ∧ ∈ z a ∈ y z
qed

term union (a)
  name (Arbitrary Union)
  format (⋃a)
  (∀ x ↔ ∈ x _ ∃ y ∧ ∈ x y ∈ y a)

theorem Element of Arbitrary Union
premise ∈ a union A
prove ∀ y ↔ ∈ y union A ∃ x ∧ ∈ y x ∈ x A
prove ∃ x ∧ ∈ a x ∈ x A
qed

theorem Element of Element Is Element of Union
premise ∈ b a
premise ∈ a A
prove ∧ ∈ b a ∈ a A
prove ∃ x ∧ ∈ b x ∈ x A
prove ∀ y ↔ ∈ y union A ∃ x ∧ ∈ y x ∈ x A
prove ∈ b union A
qed

term ∪ (a b)
  (= _ union pair a b)

theorem Membership Condition for Binary Union
prove ∀ x ↔ ∈ x union pair a b ∃ y ∧ ∈ x y ∈ y pair a b
prove ∀ x ↔ ∈ x ∪ a b ∃ y ∧ ∈ x y ∈ y pair a b
take x {
  assume ∃ y ∧ ∈ x y ∈ y pair a b {
    let y ∧ ∈ x y ∈ y pair a b {
      prove ∨ = y a = y b
      assume = y a {
        prove ∈ x a
      }
      assume = y b {
        prove ∈ x b
      }
      prove ∨ ∈ x a ∈ x b
    }
  }
  assume ∈ x a {
    prove ∈ a pair a b
    prove ∃ y ∧ ∈ x y ∈ y pair a b
  }
  assume ∈ x b {
    prove ∈ b pair a b
    prove ∃ y ∧ ∈ x y ∈ y pair a b
  }
  assume ∨ ∈ x a ∈ x b {
    prove ∃ y ∧ ∈ x y ∈ y pair a b
  }
  prove ↔ ∃ y ∧ ∈ x y ∈ y pair a b ∨ ∈ x a ∈ x b
}
prove ∀ x ↔ ∈ x ∪ a b ∨ ∈ x a ∈ x b
qed

theorem Element of Binary Union
premise ∈ c ∪ a b
prove ∀ x ↔ ∈ x ∪ a b ∨ ∈ x a ∈ x b
prove ∨ ∈ c a ∈ c b
qed

theorem Binary Union Is Symmetric
prove ∀ x ↔ ∈ x ∪ a b ∨ ∈ x a ∈ x b
prove ∀ x ↔ ∈ x ∪ b a ∨ ∈ x b ∈ x a
prove ∀ x ↔ ∨ ∈ x a ∈ x b ∨ ∈ x b ∈ x a
prove = ∪ a b ∪ b a
qed

theorem Element Transfers to Binary Union
premise ∈ c a
prove ∀ x ↔ ∈ x ∪ a b ∨ ∈ x a ∈ x b
prove ∨ ∈ c a ∈ c b
prove ∈ c ∪ a b
qed

theorem Element Transfers to Binary Union
premise ∈ c b
prove ∀ x ↔ ∈ x ∪ a b ∨ ∈ x a ∈ x b
prove ∨ ∈ c a ∈ c b
prove ∈ c ∪ a b
qed

theorem The Empty Set Is an Identity for Binary Union
prove ∀ x ↔ ∈ x ∪ ∅ a ∨ ∈ x ∅ ∈ x a
prove ∀ x ¬ ∈ x ∅
prove ∀ x ↔ ∨ ∈ x ∅ ∈ x a ∈ x a
prove ∀ x ↔ ∈ x ∪ ∅ a ∈ x a
prove = ∪ ∅ a a
qed

theorem The Empty Set Is an Identity for Binary Union
prove ∀ x ↔ ∈ x ∪ a ∅ ∨ ∈ x a ∈ x ∅
prove ∀ x ¬ ∈ x ∅
prove ∀ x ↔ ∨ ∈ x a ∈ x ∅ ∈ x a
prove ∀ x ↔ ∈ x ∪ a ∅ ∈ x a
prove = ∪ a ∅ a
qed

theorem A Union of a Non-Empty Set Is Non-Empty
premise ¬ = a ∅
prove ∃ x ∈ x a
let x ∈ x a {
  prove ∈ x ∪ a b
  prove ¬ = ∪ a b ∅
}
qed

theorem A Union of a Non-Empty Set Is Non-Empty
premise ¬ = a ∅
prove ∃ x ∈ x a
let x ∈ x a {
  prove ∈ x ∪ b a
  prove ¬ = ∪ b a ∅
}
qed

theorem Set Is in One Part of a Binary Union
premise ∈ a ∪ b c
premise ¬ ∈ a b
prove ∨ ∈ a b ∈ a c
prove ∈ a c
qed

theorem Set Is in One Part of a Binary Union
premise ∈ a ∪ b c
premise ¬ ∈ a c
prove ∨ ∈ a b ∈ a c
prove ∈ a b
qed

theorem Union Membership Equivalence If Not In One Set
premise ¬ ∈ a c
assume ∈ a b {
  prove ∈ a ∪ b c
}
assume ∈ a ∪ b c {
  prove ∈ a b
}
prove ↔ ∈ a b ∈ a ∪ b c
qed

theorem Union Membership Equivalence If Not In One Set
premise ¬ ∈ a b
assume ∈ a c {
  prove ∈ a ∪ b c
}
assume ∈ a ∪ b c {
  prove ∈ a c
}
prove ↔ ∈ a c ∈ a ∪ b c
qed

theorem Union of Subsets Is a Subset
premise ⊆ A X
premise ⊆ B X
prove ∀ x → ∈ x A ∈ x X
prove ∀ x → ∈ x B ∈ x X
prove ∀ x → ∨ ∈ x A ∈ x B ∈ x X
prove ∀ x ↔ ∈ x ∪ A B ∨ ∈ x A ∈ x B
prove ∀ x → ∈ x ∪ A B ∈ x X
prove ⊆ ∪ A B X
qed

theorem Subset Transfers to Union
premise ⊆ X A
take x {
  assume ∈ x X {
    prove ∈ x A
    prove ∈ x ∪ A B
  }
}
prove ⊆ X ∪ A B
qed

theorem Subset Transfers to Union
premise ⊆ X A
take x {
  assume ∈ x X {
    prove ∈ x A
    prove ∈ x ∪ B A
  }
}
prove ⊆ X ∪ B A
qed

theorem Set Is Subset of Its Union
prove ⊆ A A
prove ⊆ A ∪ A B
qed

theorem Set Is Subset of Its Union
prove ⊆ B B
prove ⊆ B ∪ A B
qed

theorem Redundant Union with Subset
premise ⊆ A B
take x {
  assume ∈ x ∪ A B {
    prove ∨ ∈ x A ∈ x B
    assume ∈ x A {
      prove ∈ x B
    }
    prove ∈ x B
  }
  assume ∈ x B {
    prove ∈ x ∪ A B
  }
}
prove = ∪ A B B
qed

theorem Redundant Union with Subset
premise ⊆ A B
prove = ∪ A B B
prove = ∪ B A ∪ A B
prove = ∪ B A B
qed

theorem Redundant Union with Singleton
premise ∈ a A
prove ⊆ singleton a A
prove = ∪ A singleton a A
qed

theorem Union of Subsets Is Subset
premise ⊆ A powerSet B
take y {
  assume ∈ y union A {
    prove ∃ a ∧ ∈ y a ∈ a A
    let a ∧ ∈ y a ∈ a A {
      prove ∈ a powerSet B
      prove ⊆ a B
      prove ∈ y B
    }
  }
}
prove ⊆ union A B
qed
