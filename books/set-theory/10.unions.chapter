Arbitrary and binary unions.

axiom Axiom of Union
conclusion ∃ x ∀ y ↔ ∈ $0 $$0 ∃ z ∧ ∈ $0 a ∈ $$0 $0

theorem Arbitrary Unions Exist
prove ∃ x ∀ y ↔ ∈ $0 $$0 ∃ z ∧ ∈ $0 a ∈ $$0 $0
prove ∃! x ∀ y ↔ ∈ $0 $$0 ∃ z ∧ ∈ $0 a ∈ $$0 $0
qed

term union (a)
  name (Arbitrary Union)
  format (⋃a)
  (∀ x ↔ ∈ $0 $$0 ∃ y ∧ ∈ $$0 $0 ∈ $0 a)

theorem Element of Arbitrary Union
premise ∈ a union A
prove ∀ y ↔ ∈ $0 union A ∃ x ∧ ∈ $$0 $0 ∈ $0 A
prove ∃ x ∧ ∈ a $0 ∈ $0 A
qed

theorem Element of Element Is Element of Union
premise ∈ b a
premise ∈ a A
prove ∃ x ∧ ∈ b $0 ∈ $0 A
prove ∀ y ↔ ∈ $0 union A ∃ x ∧ ∈ $$0 $0 ∈ $0 A
prove ∈ b union A
qed

term ∪ (a b)
  (= $0 union pair a b)

theorem Membership Condition for Binary Union
prove ∀ x ↔ ∈ $0 union pair a b ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b
prove ∀ x ↔ ∈ $0 ∪ a b ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b
  via = ∪ a b union pair a b
take x {
  assume ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b {
    let y ∧ ∈ $$0 $0 ∈ $0 pair a b {
      prove ∨ = $0 a = $0 b
      assume = $0 a {
        prove ∈ $$0 a
      }
      assume = $0 b {
        prove ∈ $$0 b
      }
      prove ∨ ∈ $$0 a ∈ $$0 b
    }
  }
  assume ∈ $0 a {
    prove ∈ a pair a b
    prove ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b
  }
  assume ∈ $0 b {
    prove ∈ b pair a b
    prove ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b
  }
  assume ∨ ∈ $0 a ∈ $0 b {
    prove ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b
  }
  prove ↔ ∃ y ∧ ∈ $$0 $0 ∈ $0 pair a b ∨ ∈ $0 a ∈ $0 b
}
prove ∀ x ↔ ∈ $0 ∪ a b ∨ ∈ $0 a ∈ $0 b
qed

theorem Element of Binary Union
premise ∈ c ∪ a b
prove ∀ x ↔ ∈ $0 ∪ a b ∨ ∈ $0 a ∈ $0 b
prove ∨ ∈ c a ∈ c b
qed

theorem Binary Union Is Symmetric
prove ∀ x ↔ ∈ $0 ∪ a b ∨ ∈ $0 a ∈ $0 b
prove ∀ x ↔ ∈ $0 ∪ b a ∨ ∈ $0 b ∈ $0 a
prove ∀ x ↔ ∨ ∈ $0 a ∈ $0 b ∨ ∈ $0 b ∈ $0 a
prove = ∪ a b ∪ b a
qed

theorem Element Transfers to Binary Union
premise ∈ c a
prove ∀ x ↔ ∈ $0 ∪ a b ∨ ∈ $0 a ∈ $0 b
prove ∈ c ∪ a b
qed

theorem Element Transfers to Binary Union
premise ∈ c b
prove ∀ x ↔ ∈ $0 ∪ a b ∨ ∈ $0 a ∈ $0 b
prove ∈ c ∪ a b
qed

theorem The Empty Set Is an Identity for Binary Union
prove ∀ x ↔ ∈ $0 ∪ ∅ a ∨ ∈ $0 ∅ ∈ $0 a
prove ∀ x ¬ ∈ $0 ∅
prove ∀ x ↔ ∨ ∈ $0 ∅ ∈ $0 a ∈ $0 a
prove ∀ x ↔ ∈ $0 ∪ ∅ a ∈ $0 a
prove = ∪ ∅ a a
qed

theorem The Empty Set Is an Identity for Binary Union
prove ∀ x ↔ ∈ $0 ∪ a ∅ ∨ ∈ $0 a ∈ $0 ∅
prove ∀ x ¬ ∈ $0 ∅
prove ∀ x ↔ ∨ ∈ $0 a ∈ $0 ∅ ∈ $0 a
prove ∀ x ↔ ∈ $0 ∪ a ∅ ∈ $0 a
prove = ∪ a ∅ a
qed

theorem A Union of a Non-Empty Set Is Non-Empty
premise ¬ = a ∅
prove ∃ x ∈ $0 a
let x ∈ $0 a {
  prove ∈ $0 ∪ a b
  prove ¬ = ∪ a b ∅
}
qed

theorem A Union of a Non-Empty Set Is Non-Empty
premise ¬ = a ∅
prove ∃ x ∈ $0 a
let x ∈ $0 a {
  prove ∈ $0 ∪ b a
  prove ¬ = ∪ b a ∅
}
qed

theorem Set Is in One Part of a Binary Union
premise ∈ a ∪ b c
premise ¬ ∈ a b
prove ∨ ∈ a b ∈ a c
prove ∈ a c
qed

theorem Set Is in One Part of a Binary Union
premise ∈ a ∪ b c
premise ¬ ∈ a c
prove ∨ ∈ a b ∈ a c
prove ∈ a b
qed

theorem Union Membership Equivalence If Not In One Set
premise ¬ ∈ a c
assume ∈ a b {
  prove ∈ a ∪ b c
}
assume ∈ a ∪ b c {
  prove ∈ a b
}
prove ↔ ∈ a b ∈ a ∪ b c
qed

theorem Union Membership Equivalence If Not In One Set
premise ¬ ∈ a b
assume ∈ a c {
  prove ∈ a ∪ b c
}
assume ∈ a ∪ b c {
  prove ∈ a c
}
prove ↔ ∈ a c ∈ a ∪ b c
qed

theorem Union of Subsets Is a Subset
premise ⊆ A X
premise ⊆ B X
prove ∀ x → ∈ $0 A ∈ $0 X
prove ∀ x → ∈ $0 B ∈ $0 X
prove ∀ x → ∨ ∈ $0 A ∈ $0 B ∈ $0 X
prove ∀ x ↔ ∈ $0 ∪ A B ∨ ∈ $0 A ∈ $0 B
prove ∀ x → ∈ $0 ∪ A B ∈ $0 X
prove ⊆ ∪ A B X
qed

theorem Subset Transfers to Union
premise ⊆ X A
take x {
  assume ∈ $0 X {
    prove ∈ $0 A
    prove ∈ $0 ∪ A B
  }
}
prove ⊆ X ∪ A B
qed

theorem Subset Transfers to Union
premise ⊆ X A
take x {
  assume ∈ $0 X {
    prove ∈ $0 A
    prove ∈ $0 ∪ B A
  }
}
prove ⊆ X ∪ B A
qed

theorem Set Is Subset of Its Union
prove ⊆ A A
prove ⊆ A ∪ A B
qed

theorem Set Is Subset of Its Union
prove ⊆ B B
prove ⊆ B ∪ A B
qed

theorem Redundant Union with Subset
premise ⊆ A B
take x {
  assume ∈ $0 ∪ A B {
    prove ∨ ∈ $0 A ∈ $0 B
    assume ∈ $0 A {
      prove ∈ $0 B
    }
    prove ∈ $0 B
  }
  assume ∈ $0 B {
    prove ∈ $0 ∪ A B
  }
}
prove = ∪ A B B
qed

theorem Redundant Union with Subset
premise ⊆ A B
prove = ∪ A B B
prove = ∪ B A ∪ A B
prove = ∪ B A B
qed

theorem Redundant Union with Singleton
premise ∈ a A
prove ⊆ singleton a A
prove = ∪ A singleton a A
qed

theorem Union of Subsets Is Subset
premise ⊆ A powerSet B
take y {
  assume ∈ $0 union A {
    prove ∃ a ∧ ∈ $$0 $0 ∈ $0 A
    let a ∧ ∈ $$0 $0 ∈ $0 A {
      prove ∈ $0 powerSet B
      prove ⊆ $0 B
      prove ∈ $$0 B
    }
  }
}
prove ⊆ union A B
qed
