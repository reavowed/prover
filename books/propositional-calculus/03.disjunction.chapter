chapter Disjunction
Introduce the "or" or "disjuction" connective, ∨.

statement ∨ (φ ψ) definition (→ ¬ φ ψ)

theorem Add Right Disjunct
expansion
premise φ
prove → ¬ φ ψ
prove ∨ φ ψ
qed

theorem Add Left Disjunct
expansion
premise ψ
prove → ¬ φ ψ
prove ∨ φ ψ
qed

theorem Law of the Excluded Middle
prove → ¬ φ ¬ φ
prove ∨ φ ¬ φ
qed

theorem Proof by Cases
premise ∨ φ ψ
premise → φ χ
premise → ψ χ
prove → ¬ φ ψ
prove → ¬ φ χ
prove χ
qed

theorem Proof by Cases
premise ∨ φ ψ
premise proves φ χ
premise proves ψ χ
prove → φ χ
prove → ψ χ
prove χ
qed

theorem Proof by Separate Cases
premise ∨ φ ψ
premise proves φ χ
premise proves ψ ω
assume φ {
  prove χ
  prove ∨ χ ω
}
assume ψ {
  prove ω
  prove ∨ χ ω
}
prove ∨ χ ω
qed

theorem Proof by Separate Cases
premise proves φ ψ
premise proves ¬ φ χ
prove ∨ φ ¬ φ
prove ∨ ψ χ
qed

theorem Combine Cases
premise → φ χ
premise → ψ χ
assume ∨ φ ψ {
  prove χ
}
prove → ∨ φ ψ χ
qed

theorem Combine Cases
premise proves φ χ
premise proves ψ χ
assume ∨ φ ψ {
  prove χ
}
prove → ∨ φ ψ χ
qed

theorem Reverse Disjunction
premise ∨ φ ψ
assume φ {
  prove ∨ ψ φ
}
assume ψ {
  prove ∨ ψ φ
}
prove ∨ ψ φ
qed

theorem One Disjunct Must Be True
premise ∨ φ ψ
premise ¬ φ
prove → ¬ φ ψ
prove ψ
qed

theorem One Disjunct Must Be True
premise ∨ φ ψ
premise ¬ ψ
prove ∨ ψ φ
prove φ
qed

theorem One Disjunct Must Be True
premise ∨ ¬ φ ψ
premise φ
prove ¬ ¬ φ
prove ψ
qed

theorem One Disjunct Must Be True
premise ∨ φ ¬ ψ
premise ψ
prove ¬ ¬ ψ
prove φ
qed

theorem Disjunction of Two Falsehoods Is False
premise ¬ φ
premise ¬ ψ
assume ∨ φ ψ {
  prove ψ
}
prove ¬ ∨ φ ψ
qed

theorem Deduction in Left Disjunct
premise ∨ φ ψ
premise proves φ χ
assume ψ {
  prove ψ
}
prove ∨ χ ψ
qed

theorem Deduction in Right Disjunct
premise ∨ φ ψ
premise proves ψ χ
assume φ {
  prove φ
}
prove ∨ φ χ
qed

theorem Eliminate Redundant Or
premise ∨ φ ψ
premise proves φ ψ
assume ψ {
  prove ψ
}
prove ψ
qed

theorem Eliminate Redundant Or
premise ∨ φ ψ
premise proves ψ φ
assume φ {
  prove φ
}
prove φ
qed
