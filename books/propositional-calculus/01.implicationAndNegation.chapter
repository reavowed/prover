chapter Implication and Negation
We start by introducing our two fundamental connectives, → and ¬, and proving some basic facts about them.

axiom Repetition
premise φ
conclusion φ

statement → (φ ψ) deduction

axiom Modus Ponens
premise → φ ψ elidable
premise φ
conclusion ψ

theorem Implication Is Reflexive
assume φ {
  prove φ
}
qed

theorem Implication Is Transitive
premise → φ ψ
premise → ψ χ
assume φ {
  prove ψ
  prove χ
}
qed

statement ⊥ ()

statement ¬ (φ) definition (→ φ ⊥)

axiom Remove Double Negative
premise ¬ ¬ φ
conclusion φ

theorem Self-Contradiction
premise → ¬ φ ⊥
prove ¬ ¬ φ
prove φ
qed

theorem Contradiction
premise φ
premise ¬ φ
prove → φ ⊥
prove ⊥
qed

theorem Principle of Explosion
premise ⊥
assume ¬ φ {
  prove ⊥
}
prove φ
qed

theorem Principle of Explosion
premise φ
premise ¬ φ
prove ⊥
prove ψ
qed

theorem Add Double Negative
premise φ
assume ¬ φ {
  prove ⊥
}
prove ¬ ¬ φ
qed

theorem Proof by Self-Contradiction
premise → φ ¬ φ
assume φ {
  prove ¬ φ
  prove ⊥
}
prove ¬ φ
qed

theorem Proof by Self-Contradiction
premise → ¬ φ φ
assume ¬ φ {
  prove φ
  prove ⊥
}
prove φ
qed

theorem Modus Tollens
premise → φ ψ
premise ¬ ψ
assume φ {
  prove ψ
  prove ⊥
}
prove ¬ φ
qed

theorem Modus Tollens
premise → φ ¬ ψ
premise ψ
assume φ {
  prove ¬ ψ
  prove ⊥
}
prove ¬ φ
qed

theorem Modus Tollens
premise → ¬ φ ψ
premise ¬ ψ
assume ¬ φ {
  prove ψ
  prove ⊥
}
prove φ
qed

theorem Modus Tollens
premise → ¬ φ ¬ ψ
premise ψ
assume ¬ φ {
  prove ¬ ψ
  prove ⊥
}
prove φ
qed

theorem Contraposition
premise → φ ψ
assume ¬ ψ {
  prove ¬ φ
}
qed

theorem Contraposition
premise → φ ¬ ψ
assume ψ {
  prove ¬ φ
}
qed

theorem Contraposition
premise → ¬ φ ψ
assume ¬ ψ {
  prove φ
}
qed

theorem Contraposition
premise → ¬ φ ¬ ψ
assume ψ {
  prove φ
}
qed

theorem Proof by Cases
premise → φ ψ
premise → ¬ φ ψ
assume ¬ ψ {
  prove ¬ φ
  prove ¬ ¬ φ
  prove ⊥
}
prove ψ
qed

theorem Anything Implies A True Statement
premise φ
assume ψ {
  prove φ
}
qed

theorem A False Statement Implies Anything
premise ¬ φ
assume φ {
  prove ψ
}
qed

theorem A False Statement Implies Anything
premise φ
assume ¬ φ {
  prove ψ
}
qed
