chapter Definition of the Natural Numbers
Introduce the set of natural numbers ℕ, via the successor pseudo-function S(n) and the concept of an inductive set.

term succ (term) (succ({})) (n) () (= succ n ∪ n singleton n)

theorem successorMembershipCondition Successor Membership Condition
definition-succ x
binaryUnionMembershipCondition y x singleton x
eliminateEqualsReverse 1 2 ∀ y ↔ ∈ y X ∨ ∈ y x ∈ y singleton x X
definition-singleton y x
substituteEquivalenceIntoOrLeftInsideForall 4 ∈ y x
equivalenceIsTransitiveInsideForall 3 5
qed

theorem characteriseElementsOfSuccessor Characterise Elements of Successor
successorMembershipCondition y x
eliminateForall 1 y
qed

theorem characteriseElementOfSuccessor Characterise Element of Successor
premise ∈ y succ x
characteriseElementsOfSuccessor y x
modusPonensEquivalenceLeft 1 p1
qed

theorem setIsElementOfSuccessor A Set Is an Element of Its Successor
characteriseElementsOfSuccessor x x
equalityIsReflexive x
introduceOrRight 2 ∈ x x
modusPonensEquivalenceRight 1 3
qed

theorem setIsSubsetOfSuccessor A Set Is a Subset of Its Successor
assume ∈ y' x
  characteriseElementsOfSuccessor y' x
  introduceOrLeft f.a = y' x
  modusPonensEquivalenceRight f.1 f.2
introduceImplication f.3
introduceForall 1 → ∈ y x ∈ y succ x y' y
apply-⊆ 2
qed

theorem elementOfSetIsElementOfSuccessor An Element of a Set Is an Element of Its Successor
premise ∈ y x
introduceOrLeft p1 = y x
characteriseElementsOfSuccessor y x
modusPonensEquivalenceRight 2 1
qed

statement inductive (term) ({} is inductive in parens) (X) (∧ ∈ ∅ X ∀ x → ∈ x X ∈ succ x X)

theorem successorOfElementOfInductiveSetIsElement The Successor of an Element of an Inductive Set Is an Element of the Set
premise inductive X
premise ∈ n X
unapply-inductive p1 x
eliminateAndRight 1
eliminateForall 2 n
eliminateImplication 3 p2
qed

axiom axiomOfInfinity Infinity ∃ X inductive X

theorem naturalNumbersExist The Natural Numbers Exist
axiomOfInfinity X
intersectionOverClassExists 1 S s Y
qed

theorem naturalNumbersAreUnqiue The Natural Numbers Are Unique
naturalNumbersExist S s X
setWithMembershipConditionIsUnique 1
qed


term ℕ () () () () (∀ n ↔ ∈ n ℕ ∀ X → inductive X ∈ n X )

theorem elementOfAllInductiveSetsIsNaturalNumber An Element of All Inductive Sets Is a Natural Number
premise ∀ X → inductive X ∈ n X
definition-ℕ n X
eliminateForall 1 n
modusPonensEquivalenceRight 2 p1
qed

theorem naturalNumberIsElementOfAllInductiveSets A Natural Number Is an Element of All Inductive Sets
premise ∈ n ℕ
definition-ℕ n X
eliminateForall 1 n
modusPonensEquivalenceLeft 2 p1
qed

theorem naturalNumberIsElementOfAnyInductiveSet A Natural Number Is an Element of Any Inductive Set
premise ∈ n ℕ
premise inductive X
naturalNumberIsElementOfAllInductiveSets p1 Y
eliminateForall 1 X
eliminateImplication 2 p2
qed

theorem successorOfNaturalNumberIsNaturalNumber A Successor of a Natural Number Is a Natural Number
premise ∈ n ℕ
assume inductive Y
  naturalNumberIsElementOfAnyInductiveSet p1 f.a
  successorOfElementOfInductiveSetIsElement f.a f.1
introduceImplication f.2
introduceForall 1 → inductive X ∈ succ n X Y X
elementOfAllInductiveSetsIsNaturalNumber 2
qed
