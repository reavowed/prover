book Set Theory
import Predicate Calculus

chapter Set Membership and Equality
Introduce set membership, ∈, and equality, =.

predicate ∈ 2
predicate = 2 ∀ 3 ∧ ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3

theorem substituteEqualsIntoElement Substitute equal terms into set element
hypothesis = 1 2
definition-= h1 3
eliminateForall 1 3
eliminateAndLeft 2
qed

theorem substituteEqualsIntoContainer Substitute equal terms into set container
hypothesis = 1 2
definition-= h1 3
eliminateForall 1 3
eliminateAndRight 2
qed

chapter Extensionality
Introduce the Axiom of Extensionality and explore its consequences for equality.

axiom extensionality Extensionality ∀ 1 ∀ 2 ∀ 3 → ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3

theorem setsWithSameMembersEqual Sets Are Equal If They Have The Same Members
hypothesis ∀ 3 ↔ ∈ 3 1 ∈ 3 2
extensionality 1 2 3
eliminateForall 1 1
eliminateForall 2 2
distributeForallOverImplication 3
eliminateImplication 4 h1
introduceAnd h1 5
distributeForallOverConjunction ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3 3
modusPonensEquivalenceRight 7 6
definition-= 8
qed

theorem setsWithSameMembershipConditionEqual Sets Are Equal If They Have The Same Membership Condition
hypothesis ∀ 3 ↔ ∈ 3 1 1
hypothesis ∀ 3 ↔ ∈ 3 2 1
eliminateForall h1 3
eliminateForall h2 3
reverseEquivalence 2
equivalenceIsTransitive 1 3
introduceForall 4 ↔ ∈ 3 1 ∈ 3 2 3 3
setsWithSameMembersEqual 5
qed

chapter Uniqueness Quantifier
Introduce the uniqueness quantifier, ∃!.

quantifier ∃! ∧ ∃ 1 1 ∀ 2 ∀ 3 → ∧ sub 2 1 1 sub 3 1 1 = 2 3

theorem setWithMembershipConditionIsUnique A Set with a Membership Condition Is Unique
hypothesis ∃ 1 ∀ 4 ↔ ∈ 4 1 sub 5 1 1
assume ∧ ∀ 4 ↔ ∈ 4 2 sub 5 1 1 ∀ 4 ↔ ∈ 4 3 sub 5 1 1
  eliminateAndLeft f.h
  eliminateAndRight f.h
  setsWithSameMembershipConditionEqual f.1 f.2
introduceImplication f.3
introduceForall 1 → ∧ ∀ 4 ↔ ∈ 4 2 sub 5 1 1 ∀ 4 ↔ ∈ 4 3 sub 5 1 1 = 2 3 3 3
introduceForall 2 ∀ 3 → ∧ ∀ 4 ↔ ∈ 4 2 sub 5 1 1 ∀ 4 ↔ ∈ 4 3 sub 5 1 1 = 2 3 2 2
introduceAnd h1 3
definition-∃! 4
qed
