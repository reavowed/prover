book Set Theory
import Predicate Calculus

chapter Set Membership and Equality
Introduce set membership, ∈, and equality, =.

predicate ∈ 2
predicate = 2 ∀ 3 ∧ ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3

theorem substituteEqualsIntoElement Substitute equal terms into set element
hypothesis = 1 2
definition-= h1 3
eliminateForall 1 3
eliminateAndLeft 2
qed

theorem substituteEqualsIntoContainer Substitute equal terms into set container
hypothesis = 1 2
definition-= h1 3
eliminateForall 1 3
eliminateAndRight 2
qed

chapter Extensionality
Introduce the Axiom of Extensionality and explore its consequences for equality.

axiom extensionality Extensionality ∀ 1 ∀ 2 ∀ 3 → ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3

theorem setsWithSameMembersEqual Sets Are Equal If They Have The Same Members
hypothesis ∀ 3 ↔ ∈ 3 1 ∈ 3 2
extensionality 1 2 3
eliminateForall 1 1
eliminateForall 2 2
distributeForallOverImplication 3
eliminateImplication 4 h1
introduceAnd h1 5
distributeForallOverConjunction ↔ ∈ 3 1 ∈ 3 2 ↔ ∈ 1 3 ∈ 2 3 3
modusPonensEquivalenceRight 7 6
definition-= 8
qed

theorem setsWithSameMembershipConditionEqual Sets Are Equal If They Have The Same Membership Condition
hypothesis ∀ 3 ↔ ∈ 3 1 1
hypothesis ∀ 3 ↔ ∈ 3 2 1
eliminateForall h1 3
eliminateForall h2 3
reverseEquivalence 2
equivalenceIsTransitive 1 3
introduceForallWithoutSubstitution 4 3
setsWithSameMembersEqual 5
qed

chapter Subsets
Introduce the subset predicate, ⊆.

predicate ⊆ 2 ∀ 3 → ∈ 3 1 ∈ 3 2

theorem subsetIsReflexive Subset is Reflexive
implicationIsReflexive ∈ 2 1
introduceForallWithoutSubstitution 1 2
definition-⊆ 2
introduceForallWithoutSubstitution 3 1
qed

theorem subsetIsAntisymmetric Subset is Antisymmetric
hypothesis ⊆ 1 2
hypothesis ⊆ 2 1
definition-⊆ h1 3
definition-⊆ h2 3
introduceEquivalenceInsideForall 1 2
setsWithSameMembersEqual 3
qed

theorem subsetIsTransitive Subset is Transitive
hypothesis ⊆ 1 2
hypothesis ⊆ 2 3
definition-⊆ h1 3
definition-⊆ h2 3
implicationIsTransitiveInsideForall 1 2
definition-⊆ 3
qed

chapter Uniqueness Quantifier
Introduce the uniqueness quantifier, ∃!.

quantifier ∃! ∧ ∃ 1 1 ∀ 2 ∀ 3 → ∧ sub 2 1 1 sub 3 1 1 = 2 3

theorem setWithMembershipConditionIsUnique A Set with a Membership Condition Is Unique
hypothesis ∃ 1 ∀ 4 ↔ ∈ 4 1 sub 5 1 1
assume ∀ 4 ↔ ∈ 4 2 sub 5 1 1
  assume ∀ 4 ↔ ∈ 4 3 sub 5 1 1
    setsWithSameMembershipConditionEqual f.h f.f.h
  introduceImplication f.f.1
introduceImplication f.1
combineAntecedents 1
introduceForallWithoutSubstitution 2 3
introduceForallWithoutSubstitution 3 2
introduceAnd h1 4
definition-∃! 5
qed

chapter Empty Set
Introduce the empty set, ∅.
axiom emptySet "Empty Set" ∃ 1 ∀ 2 ¬ ∈ 2 1

theorem emptySetsAreEqual Empty Sets are Equal
hypothesis ∀ 3 ¬ ∈ 3 1
hypothesis ∀ 3 ¬ ∈ 3 2
eliminateForall h1 3 3
eliminateForall h2 3 3
falseStatementsAreEquivalent 1 2
introduceForallWithoutSubstitution 3 3
setsWithSameMembersEqual 4
qed

theorem thereIsAUniqueEmptySet There Is a Unique Empty Set
emptySet 1 2
assume ∀ 2 ¬ ∈ 2 3
  assume ∀ 2 ¬ ∈ 2 4
    emptySetsAreEqual f.h f.f.h
  introduceImplication f.f.1
introduceImplication f.1
combineAntecedents 2
introduceForallWithoutSubstitution 3 4
introduceForallWithoutSubstitution 4 3
introduceAnd 1 5
definition-∃! 6
qed

term ∅ ∀ 1 ¬ ∈ 1 ∅

theorem theEmptySetIsASubsetOfAnySet The Empty Set Is a Subset of Any Set
definition-∅ 2
eliminateForallWithoutSubstitution 1
falseImpliesAnything1 2 ∈ 2 1
introduceForallWithoutSubstitution 3 2
definition-⊆ 4
qed

theorem theOnlySubsetOfTheEmptySetIsTheEmptySet The Only Subset of the Empty Set is the Empty Set
assume ⊆ 1 ∅
  definition-⊆ f.h 2
  definition-∅ 2
  eliminateForallWithoutSubstitution f.1
  eliminateForallWithoutSubstitution f.2
  modusTollens f.3 f.4
  introduceForallWithoutSubstitution f.5 2
  emptySetsAreEqual f.6 f.2
introduceImplication f.7
assume = 1 ∅
  substituteEqualsIntoElement f.h 2
  eliminateEquivalenceForward f.1
  introduceForallWithoutSubstitution f.2 2
  definition-⊆ f.3
introduceImplication f.4
introduceEquivalence 1 2
qed

chapter Power Sets
Define the power set of a set - the set of all its subsets.

axiom axiomOfPowerset "Power Set" ∀ 1 ∃ 2 ∀ 3 ↔ ∈ 3 2 ⊆ 3 1

term powerSet term 𝒫{} ∀ 2 ↔ ∈ 2 powerSet 1 ⊆ 2 1

theorem powerSetOfEmptySetContainsExactlyEmptySet The Power Set of The Empty Set Contains Exactly the Empty Set
definition-powerSet 1 ∅
eliminateForallWithoutSubstitution 1
theOnlySubsetOfTheEmptySetIsTheEmptySet 1
equivalenceIsTransitive 2 3
qed
