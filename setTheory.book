book Set Theory
import Predicate Calculus

chapter Set Membership and Equality
Introduce set membership, ∈, and equality, =.

predicate ∈ 2

theorem substituteEqualsIntoElement Substitute equal terms into set element
premise = 1 2
assume ∈ 1 3
  eliminateEquals p1 f.a ∈ 4 3 4
introduceImplication f.1
reverseEquality p1
assume ∈ 2 3
  eliminateEquals 2 f.a ∈ 4 3 4
introduceImplication f.1
introduceEquivalence 1 3
qed

theorem substituteEqualsIntoContainer Substitute equal terms into set container
premise = 1 2
assume ∈ 3 1
  eliminateEquals p1 f.a ∈ 3 4 4
introduceImplication f.1
reverseEquality p1
assume ∈ 3 2
  eliminateEquals 2 f.a ∈ 3 4 4
introduceImplication f.1
introduceEquivalence 1 3
qed

chapter Extensionality
Introduce the Axiom of Extensionality and explore its consequences for equality.

axiom extensionality Extensionality ∀ 1 ∀ 2 → ∀ 3 ↔ ∈ 3 1 ∈ 3 2 = 1 2

theorem setsWithSameMembersEqual Sets Are Equal If They Have The Same Members
premise ∀ 3 ↔ ∈ 3 1 ∈ 3 2
extensionality 1 2 3
eliminateForall 1 1
eliminateForall 2 2
eliminateImplication 3 p1
qed

theorem setsWithSameMembershipConditionEqual Sets Are Equal If They Have The Same Membership Condition
premise ∀ 3 ↔ ∈ 3 1 1
premise ∀ 3 ↔ ∈ 3 2 1
eliminateForall p1 4
eliminateForall p2 4
equivalenceIsTransitiveRight 1 2
introduceForall 3 ↔ ∈ 3 1 ∈ 3 2 4 3
setsWithSameMembersEqual 4
qed

chapter Subsets
Introduce the subset predicate, ⊆.

predicate ⊆ 2 ∀ 3 → ∈ 3 1 ∈ 3 2

theorem subsetIsReflexive Subset is Reflexive
implicationIsReflexive ∈ 3 1
introduceForall 1 → ∈ 2 1 ∈ 2 1 3 2
apply-⊆ 2
qed

theorem subsetIsAntisymmetric Subset is Antisymmetric
premise ⊆ 1 2
premise ⊆ 2 1
unapply-⊆ p1 3
unapply-⊆ p2 3
introduceEquivalenceInsideForall 1 2
setsWithSameMembersEqual 3
qed

theorem subsetIsTransitive Subset is Transitive
premise ⊆ 1 2
premise ⊆ 2 3
unapply-⊆ p1 3
unapply-⊆ p2 3
implicationIsTransitiveInsideForall 1 2
apply-⊆ 3
qed

chapter Uniqueness Quantifier
Introduce the uniqueness quantifier, ∃!.

quantifier ∃! (∃ 2 ∀ 1 ↔ 1 = 1 2) (2 1)

theorem uniqueSetExists A Unique Set Exists
premise ∃! 1 1
unapply-∃! p1 2
assume ∀ 1 ↔ 1 = 1 3
  eliminateForall f.a 3
  equalityIsReflexive 3
  modusPonensEquivalenceRight f.1 f.2
  introduceExists f.3
introduceImplication f.4
eliminateExists 1 2 3
qed

theorem setWithMembershipConditionIsUnique A Set With a Membership Condition Is Unique
premise ∃ 1 ∀ 2 ↔ ∈ 2 1 1
assume ∀ 2 ↔ ∈ 2 3 1
  assume ∀ 2 ↔ ∈ 2 4 1
    setsWithSameMembershipConditionEqual f.f.a f.a
  introduceImplication f.f.1
  assume = 4 3
    substituteEqualsIntoContainer f.f.a 5
    eliminateForall f.a 5 2
    equivalenceIsTransitive f.f.1 f.f.2
    introduceForall f.f.3 ↔ ∈ 2 4 1 5 2
  introduceImplication f.f.4
  introduceEquivalence f.1 f.2
  introduceForall f.3 ↔ ∀ 2 ↔ ∈ 2 1 1 = 1 3 4 1
  introduceExists f.4 ∀ 1 ↔ ∀ 2 ↔ ∈ 2 1 1 = 1 4 3 4
  apply-∃! f.5
introduceImplication f.6
eliminateExists p1 1 3
qed

theorem thereExistsAUniqueSetEqualToAnySet There Exists a Unique Set Equal to Any Set
equivalenceIsReflexive = 3 2
introduceForall 1 ↔ = 1 2 = 1 2 3 1
introduceExists 2 ∀ 1 ↔ = 1 2 = 1 3 2 3
apply-∃! 3
qed

chapter Empty Set
Introduce the empty set, ∅.
axiom emptySetExists "Empty Set" ∃ 1 ∀ 2 ¬ ∈ 2 1

theorem emptySetsAreEqual Empty Sets are Equal
premise ∀ 3 ¬ ∈ 3 1
premise ∀ 3 ¬ ∈ 3 2
eliminateForall p1 4 3
eliminateForall p2 4 3
falseStatementsAreEquivalent 1 2
introduceForall 3 ↔ ∈ 3 1 ∈ 3 2 4 3
setsWithSameMembersEqual 4
qed

theorem thereIsAUniqueEmptySet There Is a Unique Empty Set
emptySetExists 1 2
assume ∀ 2 ¬ ∈ 2 3
  assume ∀ 2 ¬ ∈ 2 4
    emptySetsAreEqual f.f.a f.a
  introduceImplication f.f.1
  assume = 4 3
    substituteEqualsIntoContainer f.f.a 5 4
    negateEquivalence f.f.1
    eliminateForall f.a 5 3
    modusPonensEquivalenceRight f.f.2 f.f.3
    introduceForall f.f.4 ¬ ∈ 2 4 5 2
  introduceImplication f.f.5
  introduceEquivalence f.1 f.2
  introduceForall f.3 ↔ ∀ 2 ¬ ∈ 2 1 = 1 3 4 1
  introduceExists f.4 ∀ 1 ↔ ∀ 2 ¬ ∈ 2 1 = 1 3 3 3
  apply-∃! f.5
introduceImplication f.6
eliminateExists 1 2 3
qed

term ∅ ∀ 1 ¬ ∈ 1 ∅

theorem anEmptySetIsTheEmptySet An Empty Set Is The Empty Set
premise ∀ 2 ¬ ∈ 2 1
definition-∅ 2
emptySetsAreEqual p1 1
qed

theorem theEmptySetIsASubsetOfAnySet The Empty Set Is a Subset of Any Set
definition-∅ 1
eliminateForall 1 2 1
falseImpliesAnything1 2 ∈ 2 1
introduceForall 3 → ∈ 3 ∅ ∈ 3 1 2 3
apply-⊆ 4
qed

theorem theOnlySubsetOfTheEmptySetIsTheEmptySet The Only Subset of the Empty Set is the Empty Set
assume ⊆ 1 ∅
  unapply-⊆ f.a 2
  definition-∅ 2
  eliminateForall f.1 3
  eliminateForall f.2 3
  modusTollens1 f.3 f.4
  introduceForall f.5 ¬ ∈ 2 1 3 2
  emptySetsAreEqual f.6 f.2
introduceImplication f.7
assume = 1 ∅
  substituteEqualsIntoContainer f.a 3
  eliminateEquivalenceForward f.1
  introduceForall f.2 → ∈ 2 1 ∈ 2 ∅ 3 2
  apply-⊆ f.3
introduceImplication f.4
introduceEquivalence 1 2
qed

chapter Power Sets
Define the power set of a set - the set of all its subsets.

axiom axiomOfPowerset "Power Set" ∀ 1 ∃ 2 ∀ 3 ↔ ∈ 3 2 ⊆ 3 1

term powerSet term 𝒫{} ∀ 2 ↔ ∈ 2 powerSet 1 ⊆ 2 1

theorem elementOfPowersetIsSubset An Element of a Power Set is a Subset
premise ∈ 2 powerSet 1
definition-powerSet 2 1
eliminateForall 1 2 2
modusPonensEquivalenceLeft 2 p1
qed

theorem subsetIsElementOfPowerset A Subset is An Element of a Power Set
premise ⊆ 2 1
definition-powerSet 2 1
eliminateForall 1 2 2
modusPonensEquivalenceRight 2 p1
qed

theorem powerSetOfEmptySetContainsEmptySet The Power Set of The Empty Set Contains the Empty Set
theEmptySetIsASubsetOfAnySet ∅
subsetIsElementOfPowerset 1
qed

theorem powerSetOfEmptySetContainsExactlyEmptySet The Power Set of The Empty Set Contains Exactly the Empty Set
definition-powerSet 1 ∅
eliminateForall 1 1 1
theOnlySubsetOfTheEmptySetIsTheEmptySet 1
equivalenceIsTransitive 2 3
qed

chapter Replacement
Introduce the Axiom of Replacement.

axiom axiomOfReplacement Replacement → ∀ 3 → ∈ 3 1 ∃! 4 1 ∃ 2 ∀ 4 ↔ ∈ 4 2 ∃ 3 ∧ ∈ 3 1 1

chapter Singletons
Establish the existence of the singleton set {x}.

theorem singletonsExist Singletons Exist
thereExistsAUniqueSetEqualToAnySet 3 1
anythingImpliesTrue 1 ∈ 4' powerSet ∅
introduceForall 2 → ∈ 4 powerSet ∅ ∃! 3 = 3 1 4' 4
axiomOfReplacement = 3 1 4 powerSet ∅ 3 2
eliminateImplication 4 3
assume ∃ 4 ∧ ∈ 4 powerSet ∅ = 3' 1
  introduceImplication eliminateAndRight ∧ ∈ 4' powerSet ∅ = 3' 1
  eliminateExists f.a f.1 4'
introduceImplication f.2
assume = 3' 1
  powerSetOfEmptySetContainsEmptySet
  introduceAnd f.1 f.a
  introduceExists f.2 ∧ ∈ 4 powerSet ∅ = 3' 1 ∅ 4
introduceImplication f.3
introduceEquivalence 6 7
assume ∀ 3 ↔ ∈ 3 2' ∃ 4 ∧ ∈ 4 powerSet ∅ = 3 1
  eliminateForall f.a 3'
  equivalenceIsTransitive f.1 8
  introduceForall f.2 ↔ ∈ 3 2' = 3 1 3' 3
  introduceExists f.3 ∀ 3 ↔ ∈ 3 2 = 3 1 2' 2
introduceImplication f.4
eliminateExists 5 9 2'
qed

theorem singletonsAreUnique Singletons are Unique
singletonsExist 2 3 1
setWithMembershipConditionIsUnique 1
qed

term singleton term {{}} ∀ 2 ↔ ∈ 2 singleton 1 = 2 1

theorem setIsElementOfSingleton A Set is an Element of Its Singleton
definition-singleton 2 1
eliminateForall 1 1
equalityIsReflexive 1
modusPonensEquivalenceRight 2 3
qed

theorem elementOfSingletonIsSet An Element of a Singleton is the Set Itself
premise ∈ 2 singleton 1
definition-singleton 2 1
eliminateForall 1 2
modusPonensEquivalenceLeft 2 p1
qed
